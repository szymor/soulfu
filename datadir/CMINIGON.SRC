#define SMOKE_COLOR (128)

//-----------------------------------------------------------------------
int ButtonEvent()
  int self, event, spin, subtype, rider, child
  float angle, x, y, z, vx, vy, vz


  self = FindSelf()
  rider = FindByIndex(self.rider)
  subtype = self.eqcol01 & 128
  event = self.event & 7
  if(rider)
    if((self.event & 8))
      ButtonHandled()
    else
      if(STANDARD.RiderDismountButton())
        return 0
      if(event == BUTTON_LEFT || event == BUTTON_RIGHT || event == BUTTON_SPECIAL1 || event == BUTTON_SPECIAL2)
        if(self.rltimer == 0)
          if(self.eqcol23 < 160)
//            // Cost us one ammo to fire...
//            if(CharacterHasItem(rider, ITEM_AMMO_10, TRUE))

            // Safe to fire...  Heat up a little...
            self.rltimer = 10
            self.eqcol23 = self.eqcol23 + 20
            self.action = ACTION_BASH_LEFT


            x = self.x + (3.0*self.frontx)
            y = self.y + (3.0*self.fronty)
            z = self.z + 2.2
            AutoAim(10.0, 0.0, self.spin, rider.team, rider.dex, 8192, AUTOAIM_CRUNCH)
            vx = AutoAim(0.0, 0.0, 0, 0, 0, 0, AUTOAIM_X)*0.1
            vy = AutoAim(0.0, 0.0, 0, 0, 0, 0, AUTOAIM_Y)*0.1
            vz = AutoAim(0.0, 0.0, 0, 0, 0, 0, AUTOAIM_Z)*0.1


            // Do the sound...
            PlaySound("FILE:RECON.RAW", 256, 255)
            PRECON.FrameEvent(x+(vx*2.0), y+(vy*2.0), z, vx*0.5, vy*0.5, vz*0.5, FindTarget())
            vx = self.frontx
            vy = self.fronty
            PCASING.Effect(2, self.x+(2.0*vx), self.y+(2.0*vy), z)
            PEXPLODE.Effect(x, y, z, (0.5*vx), (0.5*vy), 0.0, DAMAGE_FIRE, 0, 0, self.rider, rider.team, NULL, SMOKE_COLOR, 32)
            PEMBER.Effect(x, y, z, (0.050*vx), (0.050*vy), 0.0, 1.5, 192,12, "FILE:FIRE0.RGB")
            child = PEMBER.Effect(x, y, z, (0.050*vx), (0.050*vy), 0.0, 1.5, 255,20, "FILE:LITEGONN.RGB")
            if(child)
              child.lastx = x
              child.lasty = y
              child.lastz = z
              child.x = x + vx
              child.y = y + vy
              child.z = z + vz
              child.flags = PART_LIGHT_ON
              child.trail = 240
            child = PEMBER.Effect(x, y, z, (0.100*vx), (0.100*vy), 0.0, 2.5, 255,20, "FILE:LITEGONN.RGB")
            if(child)
              child.lastx = x
              child.lasty = y
              child.lastz = z
              child.x = x + vx
              child.y = y + vy
              child.z = z + vz
              child.flags = PART_LIGHT_ON
              child.trail = 200
          else
            // Overheat...
            self.eqcol23 = 240
            self.rltimer = 120
        return 0
  else
    // No rider...
    ButtonHandled()

//-----------------------------------------------------------------------
int Event()
  int self, rider, owner
  self = FindSelf()
  owner = FindOwner()


  STANDARD.DamageSoundEvent("FILE:BLOCK.RAW", NULL, NULL)


  if(owner)
    // Don't ripple or splash if attached to a kart...
    if(self.event == EVENT_HIT_WATER)
      return 0
    if(self.event == EVENT_RIPPLE)
      return 0
  if(self.event == EVENT_MOUNTED)
    rider = FindByIndex(self.rider)
    if(rider)
      self.team = rider.team
    return 0
  if(self.event == EVENT_DISMOUNTED)
    self.gotox = self.x
    self.gotoy = self.y
    self.team = TEAM_NEUTRAL
    return 0
  if(self.event == EVENT_DAMAGED)
    STANDARD.SimpleDamageEvent()
    return 0
  STANDARD.Event()
  self.vx = 0.0
  self.vy = 0.0

//-----------------------------------------------------------------------
int FrameEvent()
  STANDARD.FrameEvent()

//-----------------------------------------------------------------------
int ModelSetup()
  int self
  self = FindSelf()


  ModelAssign(self+MODEL_BASE_FILE, "FILE:MINIGON.RDY")
  ModelAssign(self+MODEL_BASE_TEX0, "FILE:HEDGEHOG.RGB")
  ModelAssign(self+MODEL_BASE_TEX1, "FILE:DULL.RGB")
  ModelAssign(self+MODEL_BASE_TEX2, "FILE:MINIGON.RGB")
  ModelAssign(self+MODEL_BASE_TEX3, "FILE:MINIGON.RGB")
  ModelAssign(self+MODEL_BASE_COLOR, WHITE)

//-----------------------------------------------------------------------
int GetName()
  int self
  self = FindSelf()
  StringClear(NAME_STRING)
  StringAppend(NAME_STRING, "G'nomish MiniGonne", 256)

//-----------------------------------------------------------------------
int Refresh()
  int self, rider, spin, r, g, b, owner, owner_script
  float x, y, z, vx, vy, front, up, yroll
  self = FindSelf()
  owner = FindOwner()
  if(owner)
    // Keep attached to our Kart...
    owner_script = SystemGet(SYS_CHARACTERSCRIPTFILE, 0, self.owner)
    if(owner_script == "FILE:CKART.RUN")
      yroll = (owner.eqright * 17)>>8  // Should range from 0 to 16
      front = (yroll-8)*0.104
      if(front < 0.0)
        front = -0.4615*front
      front = -3.0 + front
      up = (yroll-8)*0.32 + 2.8
      self.x = owner.x + (owner.frontx*front)
      self.y = owner.y + (owner.fronty*front)
      self.z = owner.z + (owner.frontz*front) + up
      // Correct position for kart velocity
      if(owner > self)
        self.x = self.x + owner.vx
        self.y = self.y + owner.vy
        self.z = self.z + owner.vz
      x = self.x - owner.x
      y = self.y - owner.y
      if((x*owner.vx + y*owner.vy) > 0.0)
        // Kart is backing up...  Don't collide with it...
        self.nctimer = 5
      self.floorz = owner.floorz
      rider = FindByIndex(self.rider)
      if(rider)
        if(rider.pttimer > 0)
          // Make gonne spin with kart...
          self.gotox = self.x
          self.gotoy = self.y
          spin = ((self.aistate<<8) | self.aimode) - owner.spin
          self.spin = self.spin - spin
        if(rider.team != owner.team && owner.team != TEAM_NEUTRAL)
          // Don't allow mismatch of rider and gunner...
          Dismount()
      else
        // Make gonne spin with kart...
        self.gotox = self.x
        self.gotoy = self.y
        spin = ((self.aistate<<8) | self.aimode) - owner.spin
        self.spin = self.spin - spin

        // Also make the gonne's team match that of the Kart...
        self.team = owner.team
      self.aistate = owner.spin>>8
      self.aimode = owner.spin&255
    else
      // Kart is no longer a kart...
      Dismount()
      GoPoof(POOF_SELF)
  else
    // No longer attached...
    if(self.owner != 65535)
      Dismount()
      GoPoof(POOF_SELF)
    self.mflags = 0



  // Heating/Cooling of MiniGonne...
  r = 255
  g = 255 - self.eqcol23
  b = g
  ModelAssign(self+MODEL_BASE_COLOR, (r<<16) | (g<<8) | b)
  if(self.eqcol23 > 1)
    self.eqcol23--
    if(self.rltimer > 20)
      self.eqcol23--
      if((self.rltimer & 3) == 0)
        vx = self.frontx
        vy = self.fronty
        x = self.x + (3.0*vx)
        y = self.y + (3.0*vy)
        z = self.z + 2.2
        PSMOKE.Effect(x, y, z, 0.0, 0.0, 0.125, ((self.rltimer - 20 + RollDice(1, 40))*0.0125), 0.04, FALSE, WHITE, self.rltimer + (self.rltimer>>1), 30 + RollDice(1, 20))




  // Don't allow turret to be destroyed...
  if(self.hits > 0)
    self.hits = self.hitsmax
    self.hitstap = 0
  self.timer = 1

//-----------------------------------------------------------------------
int Spawn()
  int self, index, owner, owner_script
  float x, y, z
  self = FindSelf()

  self.boxsize = 1.6
  self.height = 2
  self.flags = CHAR_CAN_BE_MOUNTED | CHAR_NO_BACKSTAB | CHAR_GRAVITY_ON
  self.sflags = SCRIPT_NO_KNOCK_OUT | SCRIPT_NO_PETRIFY | SCRIPT_NO_POISON
  self.oflags = OTHER_FLAG_NO_MORPH | OTHER_FLAG_NO_HEARTS
  self.vflags = VIRTUE_FLAG_NO_DROWN
  self.vspin = 600
  self.timer = 1
  self.team = TEAM_NEUTRAL
  self.eqcol23 = 0  // Overheat timer...


  self.expgive = 0
  self.hitsmax = 100
  self.hits = self.hitsmax
  ModelSetup()


  // Look for a Kart to attach to...
  index = 0
  self.owner = 65535
  while(index < MAX_CHARACTER)
    owner = FindByIndex(index)
    if(owner && owner != self)
      owner_script = SystemGet(SYS_CHARACTERSCRIPTFILE, 0, index)
      if(owner_script == "FILE:CKART.RUN")
        x = owner.x - self.x
        y = owner.y - self.y
        z = owner.z - self.z
        if((x*x + y*y + z*z) < 1.0)
          // Looks like we found a Kart...
          self.owner = index
          self.aistate = owner.spin>>8
          self.aimode = owner.spin&255
          self.spin = owner.spin+8192
          self.mflags = MORE_FLAG_PLATFALL
          index = MAX_CHARACTER
    index++
  STANDARD.PutOnMount()


//-----------------------------------------------------------------------
