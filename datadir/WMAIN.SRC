#define CHEAT (SystemGet(SYS_KEYDOWN, 6, 0))

#define button1 self.stat00l
#define button2 self.stat00h
#define button3 self.stat01h
#define button4 self.stat02l
#define button5 self.stat02h
#define button6 self.stat03l
#define button7 self.stat16h
#define button8 self.stat18l

#define flag1   self.stat03h
#define flag2   self.stat04l
#define flag3   self.stat04h
#define flag4   self.stat05l
#define flag5   self.stat05h
#define flag6   self.stat06l
#define flag7   self.stat06h
#define picked_flag self.stat07l
#define no_players  self.stat07h
#define ticker1 self.stat08w
#define ticker2 self.stat09w
#define ticker3 self.stat10w
#define ticker4 self.stat11w
#define ticker5 self.stat12w
#define ticker6 self.stat13w
#define ticker7 self.stat14w
#define ticker8 self.stat15w
#define ticker_alpha self.stat16l
#define timer   self.stat17l

#define FLASH_RATE 300


//-----------------------------------------------------------------------
ResetTickers()
  int self
  self = FindSelf()
  ticker1 = 0
  ticker2 = 8192
  ticker3 = 16384
  ticker4 = 24576
  ticker5 = 32768
  ticker6 = 40960
  ticker7 = 49152
  ticker8 = 57344

//-----------------------------------------------------------------------
DrawHighlight(int cycle, int main_alpha)
  float lx, rx
  float ty, by
  float size
  float y
  int alpha

  size = (cycle/65535.0)
  alpha = (1.0 - sin(size*360.0+90.0))*20
  alpha = (alpha*main_alpha)>>8
  y = sin(size*360.0)*0.5
  size = size*20.0
  lx = 0.0  - size
  rx = 16.0 + size
  ty = 6.0-y
  by = 8.0-y
  WindowMegaImage(lx,ty,  rx,ty,  rx,by,  lx,by,  0.0,0.0,  1.0,1.0,  white, ALPHA_LIGHT | alpha, "FILE:=GREEN.RGB")
  ty = 6.0+y
  by = 8.0+y
  WindowMegaImage(lx,ty,  rx,ty,  rx,by,  lx,by,  0.0,0.0,  1.0,1.0,  white, ALPHA_LIGHT | alpha, "FILE:=RED.RGB")
  

//-----------------------------------------------------------------------
int NumberOfLocalPlayers()
  int i
  i = 0
  if(SystemGet(SYS_PLAYERDEVICE, 0, PLAYER_DEVICE_TYPE)!=PLAYER_DEVICE_NONE)
    i++
  if(SystemGet(SYS_PLAYERDEVICE, 1, PLAYER_DEVICE_TYPE)!=PLAYER_DEVICE_NONE)
    i++
  if(SystemGet(SYS_PLAYERDEVICE, 2, PLAYER_DEVICE_TYPE)!=PLAYER_DEVICE_NONE)
    i++
  if(SystemGet(SYS_PLAYERDEVICE, 3, PLAYER_DEVICE_TYPE)!=PLAYER_DEVICE_NONE)
    i++
  return i

//-----------------------------------------------------------------------
Spawn()
  int self


  self = FindSelf()
  button1 = 0
  button2 = 0
  button3 = 0
  button4 = 0
  button5 = 0
  button6 = 0
  button7 = 0
  button8 = 0
  flag1 = 0
  flag2 = 0
  flag3 = 0
  flag4 = 0
  flag5 = 0
  flag6 = 0
  flag7 = 0
  timer = 0
  picked_flag = FileReadByte("FILE:CONFIG.DAT", 67) + 1
  no_players = FALSE
  ticker_alpha = 0

  // Message of the day...
  Spawn(WINDOW, 0.0, 0.0, 0.0, "FILE:WBUBBLE.RUN")


  // Intro...
// !!!BAD!!!
// !!!BAD!!!  Only do once...
// !!!BAD!!!
//  Spawn(WINDOW, self.x, self.y, 1.0, "FILE:WSPLASH.RUN")



//-----------------------------------------------------------------------
Refresh()
  int    i, self
  float  y, yadd, xpos, xadd
  float screenx

  screenx = SystemGet(SYS_SCREENPARAMS, 0, 2)
  xadd = screenx / 2
  xpos = xadd / 2

  // Allow us to save data in the window's properties...
  self = FindSelf()


  // Make this the topmost window...
  SystemSet(SYS_TOPWINDOW, 0, 0, 0)


  // Keep tryin' to leave any network game we're in...
  GoPoof(POOF_ALL_CHARACTERS)
  GoPoof(POOF_ALL_PARTICLES)
  SystemSet(SYS_LEAVEGAME, 0, 0, 0)


  // Button actions
  if(button1 == 250)
    StopMusic()
    i = NumberOfLocalPlayers()
    if(i > 0)
      ButtonSound()
      GoPoof(POOF_ALL_OTHER_WINDOWS)
      MAPGEN.Setup()
      MAPGEN.SetCurrentRoom(0)
      SystemSet(SYS_JOINGAME, 255, 255, 255)
      if(SystemGet(SYS_PLAYERDEVICE, 0, PLAYER_DEVICE_TYPE)!=PLAYER_DEVICE_NONE)
        Spawn(WINDOW, xpos, 80.0, 0.0, "FILE:WSPAWN.RUN")
      if(SystemGet(SYS_PLAYERDEVICE, 1, PLAYER_DEVICE_TYPE)!=PLAYER_DEVICE_NONE)
        Spawn(WINDOW, xpos + xadd, 80.0, 1.0, "FILE:WSPAWN.RUN")
      if(SystemGet(SYS_PLAYERDEVICE, 2, PLAYER_DEVICE_TYPE)!=PLAYER_DEVICE_NONE)
        Spawn(WINDOW, xpos, 210.0, 2.0, "FILE:WSPAWN.RUN")
      if(SystemGet(SYS_PLAYERDEVICE, 3, PLAYER_DEVICE_TYPE)!=PLAYER_DEVICE_NONE)
        Spawn(WINDOW, xpos + xadd, 210.0, 3.0, "FILE:WSPAWN.RUN")
      Spawn(WINDOW, screenx / 2, 280.0, 1.0, "FILE:WLEAVE.RUN")
      GoPoof(POOF_SELF)
    else
      BuzzerSound()
      no_players = TRUE
      ResetTickers()
  if(button8 == 250)
    StopMusic()
    i = NumberOfLocalPlayers()
    if(i > 0)
      ButtonSound()
      GoPoof(POOF_ALL_OTHER_WINDOWS)
      MAPGEN.Setup()
      SystemSet(SYS_JOINGAME, 255, 255, 255)
      if(SystemGet(SYS_LOAD, 1, 0))
        SystemSet(SYS_LOAD, 1, 0, 0)
        if(SystemGet(SYS_LOCALPLAYER, 0, 0) != 65535)
          Spawn(WINDOW, xpos, 80.0, 0.0, "FILE:WLOAD.RUN")
        if(SystemGet(SYS_LOCALPLAYER, 1, 0) != 65535)
          Spawn(WINDOW, xpos + xadd, 80.0, 1.0, "FILE:WLOAD.RUN")
        if(SystemGet(SYS_LOCALPLAYER, 2, 0) != 65535)
          Spawn(WINDOW, xpos, 210.0, 2.0, "FILE:WLOAD.RUN")
        if(SystemGet(SYS_LOCALPLAYER, 3, 0) != 65535)
          Spawn(WINDOW, xpos + xadd, 210.0, 3.0, "FILE:WLOAD.RUN")
      if(!SystemGet(SYS_LOAD, 1, 0))
        GoPoof(POOF_ALL_OTHER_WINDOWS)
      Spawn(WINDOW, screenx / 2, 280.0, 1.0, "FILE:WLOADER.RUN")
      GoPoof(POOF_SELF)
    else
      BuzzerSound()
      no_players = TRUE
      ResetTickers()
  if(button2 == 250)
    StopMusic()
    i = NumberOfLocalPlayers()
    if(i > 0)
      ButtonSound()
      GoPoof(POOF_ALL_OTHER_WINDOWS)
      Spawn(WINDOW, screenx / 2, 150.0, 1.0, "FILE:WNETMAP.RUN")
      GoPoof(POOF_SELF)
    else
      BuzzerSound()
      no_players = TRUE
      ResetTickers()
  if(button3 == 250)
    StopMusic()
    ButtonSound()
    GoPoof(POOF_ALL_OTHER_WINDOWS)
    Spawn(WINDOW, screenx / 2, 150.0, 1.0, "FILE:WSETTING.RUN")
    GoPoof(POOF_SELF)
  if(button4 == 250)
    StopMusic()
    ButtonSound()
    GoPoof(POOF_ALL_OTHER_WINDOWS)
    Spawn(WINDOW, xpos, 80.0, 0.0, "FILE:WCONTROL.RUN")
    Spawn(WINDOW, xpos + xadd, 80.0, 1.0, "FILE:WCONTROL.RUN")
    Spawn(WINDOW, xpos, 210.0, 2.0, "FILE:WCONTROL.RUN")
    Spawn(WINDOW, xpos + xadd, 210.0, 3.0, "FILE:WCONTROL.RUN")
    Spawn(WINDOW, screenx / 2, 280.0, 1.0, "FILE:WCLOSER.RUN")
    GoPoof(POOF_SELF)
  if(button5 == 250)
    StopMusic()
    if(SystemGet(SYS_DEVTOOL, 0, 0) && CHEAT)
      ButtonSound()
      GoPoof(POOF_ALL_OTHER_WINDOWS)
      Spawn(WINDOW, self.x, self.y, 1.0, "FILE:WMESSAGE.RUN")
      Spawn(WINDOW, self.x, self.y, 1.0, "FILE:WTOOLS.RUN")
      GoPoof(POOF_SELF)
    else
      if(SystemGet(SYS_PAYINGCUSTOMER, 0, 0) == FALSE)
        ButtonSound()
        GoPoof(POOF_ALL_OTHER_WINDOWS)
        Spawn(WINDOW, screenx / 2, 150.0, 1.0, "FILE:WMONEY.RUN")
        GoPoof(POOF_SELF)
      else
        PlaySound("FILE:-THANKS.RAW", 256, 255)
  if(button6 == 250)
    StopMusic()
    ButtonSound()
    GoPoof(POOF_ALL_OTHER_WINDOWS)
    Spawn(WINDOW, screenx / 2, 150.0, 1.0, "FILE:WHISCORE.RUN")
    GoPoof(POOF_SELF)
  if(button7 == 250)
    StopMusic()
    ButtonSound()
    if(SystemGet(SYS_NETWORKFINISHED, 0, 0))
      SystemSet(SYS_QUITGAME, 0, 0, TRUE)


  // Title bar (no drag)
  TitleBar(FALSE, 0.0, -7.0, StringLanguage(1))


  // Webpage address...
  WindowBorder(FALSE, -12.0, 14.3, 24, 1, BORDER_LOW_TITLE)
  WindowString(WHITE, 0.0, 0.1, "www.AaronBishopGames.com")


  // Copyright notice
  WindowBorder(FALSE, -15.0, 11.5, 30, 2, BORDER_BOX)
  WindowString(WHITE, 0.0, 0.0, "\127 Copyright 2007, Aaron Bishop")
  WindowString(WHITE, 15.0-(0.5*StringLength(StringLanguage(62))), 1.0, StringLanguage(62))


  // Main window
  WindowBorder(FALSE, -8.0, -7.0, 16, 16, BORDER_FULL)


  // Button images
  button1 = ButtonImage(button1, 0.0, 0.0, 16.0, 2.0, StringLanguage(15))   // Local Game
  button8 = ButtonImage(button8, 0.0, 2.0, 16.0, 2.0, StringLanguage(180))  // Load Game
  button2 = ButtonImage(button2, 0.0, 4.0, 16.0, 2.0, StringLanguage(16))   // Network Game
  button3 = ButtonImage(button3, 0.0, 6.0, 16.0, 2.0, StringLanguage(2))    // Settings
  button4 = ButtonImage(button4, 0.0, 8.0, 16.0, 2.0, StringLanguage(17))   // Controls
  if(SystemGet(SYS_DEVTOOL, 0, 0) && CHEAT)
    button5 = ButtonImage(button5, 0.0, 10.0, 16.0, 2.0, StringLanguage(181)) // Tools
  else
    if(SystemGet(SYS_PAYINGCUSTOMER, 0, 0))
      button5 = ButtonImage(button5, 0.0, 10.0, 16.0, 2.0, StringLanguage(182)) // Thanks!
    else
      if(timer & 128)
        if(timer & 64)
          button5 = ButtonImage(button5, 0.0, 10.0, 16.0, 2.0, StringLanguage(183))      // Want More?
        else
          button5 = ButtonImage(button5, 0.0, 10.0, 16.0, 2.0, StringLanguage(184))          // Be Nice!
      else
        if(timer & 64)
          button5 = ButtonImage(button5, 0.0, 10.0, 16.0, 2.0, StringLanguage(185))  // It's Free!
        else
          button5 = ButtonImage(button5, 0.0, 10.0, 16.0, 2.0, StringLanguage(184))        // Be Nice!
  button6 = ButtonImage(button6, 0.0,12.0, 16.0, 2.0, StringLanguage(186))          // High Scores
  button7 = ButtonImage(button7, 0.0,14.0, 16.0, 2.0, StringLanguage(19))   // Quit


  // Language Flags
  y = 11.0
  yadd = -2.5

  if(flag6 || picked_flag == 6)
    flag6 = WindowImage(-5.00, y, 4.00, 2.00, "FILE:=POLAND.RGB", "Polski", flag6)
    if(flag6 == 255)
      ButtonSound()
      picked_flag = 6
      FileWriteByte("FILE:CONFIG.DAT", 67, picked_flag-1)
      SystemSet(SYS_USERLANGUAGE, 0, 0, picked_flag-1)
      flag6 = 254
  else
    flag6 = WindowImage(-4.00, y+0.50, 2.00, 1.00, "FILE:=POLAND.RGB", NULL, 0)
  y = y + yadd

  if(flag5 || picked_flag == 5)
    flag5 = WindowImage(-5.00, y, 4.00, 2.00, "FILE:=GERMANY.RGB", "Deutsch", flag5)
    if(flag5 == 255)
      ButtonSound()
      picked_flag = 5
      FileWriteByte("FILE:CONFIG.DAT", 67, picked_flag-1)
      SystemSet(SYS_USERLANGUAGE, 0, 0, picked_flag-1)
      flag5 = 254
  else
    flag5 = WindowImage(-4.00, y+0.50, 2.00, 1.00, "FILE:=GERMANY.RGB", NULL, 0)
  y = y + yadd


  if(flag4 || picked_flag == 4)
    flag4 = WindowImage(-5.00, y, 4.00, 2.00, "FILE:=ITALY.RGB", "Italiano", flag4)
    if(flag4 == 255)
      ButtonSound()
      picked_flag = 4
      FileWriteByte("FILE:CONFIG.DAT", 67, picked_flag-1)
      SystemSet(SYS_USERLANGUAGE, 0, 0, picked_flag-1)
      flag4 = 254
  else
    flag4 = WindowImage(-4.00, y+0.50, 2.00, 1.00, "FILE:=ITALY.RGB", NULL, 0)
  y = y + yadd


  if(flag3 || picked_flag == 3)
    flag3 = WindowImage(-5.00, y, 4.00, 2.00, "FILE:=SPAIN.RGB", "Español", flag3)
    if(flag3 == 255)
      ButtonSound()
      picked_flag = 3
      FileWriteByte("FILE:CONFIG.DAT", 67, picked_flag-1)
      SystemSet(SYS_USERLANGUAGE, 0, 0, picked_flag-1)
      flag3 = 254
  else
    flag3 = WindowImage(-4.00, y+0.50, 2.00, 1.00, "FILE:=SPAIN.RGB", NULL, 0)
  y = y + yadd


  if(flag2 || picked_flag == 2)
    flag2 = WindowImage(-5.00, y, 4.00, 2.00, "FILE:=FRANCE.RGB", "Français", flag2)
    if(flag2 == 255)
      ButtonSound()
      picked_flag = 2
      FileWriteByte("FILE:CONFIG.DAT", 67, picked_flag-1)
      SystemSet(SYS_USERLANGUAGE, 0, 0, picked_flag-1)
      flag2 = 254
  else
    flag2 = WindowImage(-4.00, y+0.50, 2.00, 1.00, "FILE:=FRANCE.RGB", NULL, 0)
  y = y + yadd


  if(flag1 || picked_flag == 1)
    flag1 = WindowImage(-5.00, y, 4.00, 2.00, "FILE:=AMERICA.RGB", "English", flag1)
    if(flag1 == 255)
      ButtonSound()
      picked_flag = 1
      FileWriteByte("FILE:CONFIG.DAT", 67, picked_flag-1)
      SystemSet(SYS_USERLANGUAGE, 0, 0, picked_flag-1)
      flag1 = 254
  else
    flag1 = WindowImage(-4.00, y+0.50, 2.00, 1.00, "FILE:=AMERICA.RGB", NULL, 0)
  y = y + yadd


  // Highlight controls menu if we don't have any players setup...
  timer = timer + SystemGet(SYS_MAINFRAMESKIP, 0, 0)
  if(no_players)
    ticker1 = ticker1 + SystemGet(SYS_MAINFRAMESKIP, 0, 0)*FLASH_RATE
    ticker2 = ticker2 + SystemGet(SYS_MAINFRAMESKIP, 0, 0)*FLASH_RATE
    ticker3 = ticker3 + SystemGet(SYS_MAINFRAMESKIP, 0, 0)*FLASH_RATE
    ticker4 = ticker4 + SystemGet(SYS_MAINFRAMESKIP, 0, 0)*FLASH_RATE
    ticker5 = ticker5 + SystemGet(SYS_MAINFRAMESKIP, 0, 0)*FLASH_RATE
    ticker6 = ticker6 + SystemGet(SYS_MAINFRAMESKIP, 0, 0)*FLASH_RATE
    ticker7 = ticker7 + SystemGet(SYS_MAINFRAMESKIP, 0, 0)*FLASH_RATE
    ticker8 = ticker8 + SystemGet(SYS_MAINFRAMESKIP, 0, 0)*FLASH_RATE
    i = ticker_alpha + (SystemGet(SYS_MAINFRAMESKIP, 0, 0)*4)
    ticker_alpha = i
    if(ticker_alpha < i)
      ticker_alpha = 255

    DrawHighlight(ticker1, ticker_alpha)
    DrawHighlight(ticker2, ticker_alpha)
    DrawHighlight(ticker3, ticker_alpha)
    DrawHighlight(ticker4, ticker_alpha)
    DrawHighlight(ticker5, ticker_alpha)
    DrawHighlight(ticker6, ticker_alpha)
    DrawHighlight(ticker7, ticker_alpha)
    DrawHighlight(ticker8, ticker_alpha)



//-----------------------------------------------------------------------
