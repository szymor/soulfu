#define LICH_SCALE 1.5

//-----------------------------------------------------------------------
int ButtonEvent()
  STANDARD.ButtonEvent()

//-----------------------------------------------------------------------
int Event()
  int self
  self = FindSelf()
  STANDARD.Event()
  if(self.event == EVENT_DAMAGED)
    if(self.daction == ACTION_STUN || self.daction == ACTION_KNOCK_OUT)
      self.evtimer = (self.evtimer >> 1)+1

//-----------------------------------------------------------------------
int FrameEvent()
  STANDARD.FrameEvent()

//-----------------------------------------------------------------------
int ModelSetup()
  int self, subtype


  // Funky shield stuff...
  FUNKY.ShieldUnpress()
  ModelClear()


  self = FindSelf()
  subtype = (self.eqcol01>>6)
  ModelAssign(self+MODEL_BASE_FILE, "FILE:SKULANIM.RDY")
  ModelAssign(self+MODEL_BASE_TEX0, "FILE:WHITE.RGB")
  ModelAssign(self+MODEL_BASE_TEX1, "FILE:WHITE.RGB")
  ModelAssign(self+MODEL_BASE_TEX2, "FILE:WHITE.RGB")
  ModelAssign(self+MODEL_BASE_TEX3, "FILE:WHITE.RGB")
  ModelAssign(self+MODEL_BASE_COLOR, WHITE)

  ModelAssign(self+MODEL_CHEST_FILE, "FILE:SKULBODY.RDY")
  ModelAssign(self+MODEL_CHEST_TEX0, "FILE:BONE.RGB")
  ModelAssign(self+MODEL_CHEST_TEX1, "FILE:STEEL.RGB")
  ModelAssign(self+MODEL_CHEST_TEX2, "FILE:DBLACK.RGB")
  ModelAssign(self+MODEL_CHEST_TEX3, "FILE:SHADSKUL.RGB")
  ModelAssign(self+MODEL_CHEST_COLOR, WHITE)

  ModelAssign(self+MODEL_LEGS_FILE, "FILE:SKULLIMB.RDY")
  ModelAssign(self+MODEL_LEGS_TEX0, "FILE:BONE.RGB")
  ModelAssign(self+MODEL_LEGS_TEX1, "FILE:SHADSKUL.RGB")
  ModelAssign(self+MODEL_LEGS_TEX2, "FILE:SHADSKUL.RGB")
  ModelAssign(self+MODEL_LEGS_TEX3, "FILE:SHADSKUL.RGB")
  ModelAssign(self+MODEL_LEGS_COLOR, WHITE)


  if(subtype == 0)
    // Skulton
    ModelAssign(self+MODEL_HEAD_FILE, "FILE:SKULHELM.RDY")
    ModelAssign(self+MODEL_HEAD_TEX0, "FILE:BONE.RGB")
    ModelAssign(self+MODEL_HEAD_TEX1, "FILE:STEEL.RGB")
    ModelAssign(self+MODEL_HEAD_TEX2, "FILE:SHADSKUL.RGB")
    ModelAssign(self+MODEL_HEAD_TEX3, "FILE:WHITE.RGB")
    ModelAssign(self+MODEL_HEAD_COLOR, WHITE)
  if(subtype == 1)
    // Reaper
    ModelAssign(self+MODEL_HEAD_FILE, "FILE:SKULHORN.RDY")
    ModelAssign(self+MODEL_HEAD_TEX0, "FILE:BONE.RGB")
    ModelAssign(self+MODEL_HEAD_TEX1, "FILE:MITHRIL.RGB")
    ModelAssign(self+MODEL_HEAD_TEX2, "FILE:SHADSKUL.RGB")
    ModelAssign(self+MODEL_HEAD_TEX3, "FILE:WHITE.RGB")
    ModelAssign(self+MODEL_HEAD_COLOR, WHITE)

    ModelAssign(self+MODEL_CHEST_TEX1, "FILE:MITHRIL.RGB")

  if(subtype == 2)
    // Bone Mage
    ModelAssign(self+MODEL_HEAD_FILE, "FILE:SKULHEAD.RDY")
    ModelAssign(self+MODEL_HEAD_TEX0, "FILE:BONE.RGB")
    ModelAssign(self+MODEL_HEAD_TEX1, "FILE:GRED.RGB")
    ModelAssign(self+MODEL_HEAD_TEX2, "FILE:SHADSKUL.RGB")
    ModelAssign(self+MODEL_HEAD_TEX3, "FILE:WHITE.RGB")
    ModelAssign(self+MODEL_HEAD_COLOR, WHITE)

    ModelAssign(self+MODEL_CHEST_TEX1, "FILE:GRED.RGB")

  if(subtype == 3)
    // Lich King
    ModelAssign(self+MODEL_HEAD_FILE, "FILE:SKULKING.RDY")
    ModelAssign(self+MODEL_HEAD_TEX0, "FILE:BONE.RGB")
    ModelAssign(self+MODEL_HEAD_TEX1, "FILE:GOLD.RGB")
    ModelAssign(self+MODEL_HEAD_TEX2, "FILE:SHADSKUL.RGB")
    ModelAssign(self+MODEL_HEAD_TEX3, "FILE:WHITE.RGB")
    ModelAssign(self+MODEL_HEAD_COLOR, WHITE)

    ModelAssign(self+MODEL_CHEST_TEX1, "FILE:GOLD.RGB")
    ModelAssign(self+MODEL_CHEST_TEX0, "FILE:GWHITE.RGB")
    ModelAssign(self+MODEL_LEGS_TEX0, "FILE:GWHITE.RGB")
    ModelAssign(self+MODEL_HEAD_TEX0, "FILE:GWHITE.RGB")

  STANDARD.ItemModelSetup(MODEL_LEFT_FILE)
  STANDARD.ItemModelSetup(MODEL_RIGHT_FILE)



  // Mirror shield override
  if((self.eqleft & 252) == ITEM_MIRROR_SHIELD)
    if(subtype == 0)
      ModelAssign(self+MODEL_LEFT_TEX0, "FILE:STEEL.RGB")
      ModelAssign(self+MODEL_LEFT_TEX2, "FILE:DSTEEL.RGB")
    if(subtype == 1)
      ModelAssign(self+MODEL_LEFT_TEX0, "FILE:MITHRIL.RGB")
      ModelAssign(self+MODEL_LEFT_TEX2, "FILE:DSTEEL.RGB")
    if(subtype == 2)
      ModelAssign(self+MODEL_LEFT_TEX0, "FILE:GRED.RGB")
      ModelAssign(self+MODEL_LEFT_TEX2, "FILE:DRED.RGB")
    if(subtype == 3)
      ModelAssign(self+MODEL_LEFT_TEX0, "FILE:GOLD.RGB")
      ModelAssign(self+MODEL_LEFT_TEX2, "FILE:DGOLD.RGB")
  // Great Sword/Broad Axe override
  if(subtype == 1)
    if((self.eqleft & 248) == ITEM_GREAT_SWORD)
      ModelAssign(self+MODEL_LEFT_TEX1, "FILE:=MSWORD.RGB")
    if((self.eqright & 248) == ITEM_GREAT_SWORD)
      ModelAssign(self+MODEL_RIGHT_TEX1, "FILE:=MSWORD.RGB")
    if((self.eqleft & 248) == ITEM_BROAD_AXE)
      ModelAssign(self+MODEL_LEFT_TEX3, "FILE:MITHRIL.RGB")
    if((self.eqright & 248) == ITEM_BROAD_AXE)
      ModelAssign(self+MODEL_RIGHT_TEX3, "FILE:MITHRIL.RGB")



//-----------------------------------------------------------------------
int GetName()
  int self, subtype

  self = FindSelf()
  subtype = (self.eqcol01>>6)
  StringClear(NAME_STRING)
  if(subtype == 0)
    StringAppend(NAME_STRING, "Skulton", 256)
  if(subtype == 1)
    StringAppend(NAME_STRING, "Reaper", 256)
  if(subtype == 2)
    StringAppend(NAME_STRING, "Bone Mage", 256)
  if(subtype == 3)
    StringAppend(NAME_STRING, "Lich King", 256)
  STANDARD.NameAppendHits()

//-----------------------------------------------------------------------
int AIScript()
  STANDARD.AIUseMeat()
  STANDARD.AIScript()

//-----------------------------------------------------------------------
int Refresh()
  int self
  self = FindSelf()
  self.frontx = self.frontx*LICH_SCALE
  self.fronty = self.fronty*LICH_SCALE
  self.frontz = self.frontz*LICH_SCALE
  self.sidex = self.sidex*LICH_SCALE
  self.sidey = self.sidey*LICH_SCALE
  self.sidez = self.sidez*LICH_SCALE
  self.upx = self.upx*LICH_SCALE
  self.upy = self.upy*LICH_SCALE
  self.upz = self.upz*LICH_SCALE
  self.timer = 1

//-----------------------------------------------------------------------
int Spawn()
  int self, subtype
  float difficulty
  self = FindSelf()
  subtype = (self.eqcol01>>6)

  self.boxsize = 1.5
  self.height = 6
  self.flags = CHAR_GRAVITY_ON | CHAR_NO_BACKSTAB | CHAR_CAN_RIDE_MOUNT
  self.oflags = OTHER_FLAG_NO_HEARTS
  self.sflags = SCRIPT_NO_PETRIFY | SCRIPT_NO_POISON
  self.vspin = 1400
  self.timer = 0
  self.eqspec1 = ITEM_JUMP


  difficulty = STANDARD.SubtypeDifficulty(subtype, 1.5)
  STANDARD.TreasureSetup(difficulty, 10, 1.1, ITEM_HEAL, ITEM_COOKIE)
  self.hitsmax = (11*difficulty)+10
  self.hits = self.hitsmax
  self.str = (14*difficulty)+6
  self.dex = (12*difficulty)+8
  self.int = 0

  self.defacid = 50
  self.defedge = 0
  self.defbash = -5+subtype
  self.deffire = subtype+1
  self.defice = subtype+1
  self.defvolt = subtype+1
  self.defwoun = 50

  if(subtype == 0)
    self.eqcol23 = 8<<4 // shield color
    if(RollDice(1, 20)&1)
      self.eqleft = ITEM_BOW
      self.eqright = ITEM_SHORT_SWORD
      self.item00 = ITEM_QUIVER_10
    else
      self.eqleft = ITEM_SHIELD
      if(RollDice(1, 20)&1)
        self.eqright = ITEM_SWORD
      else
        self.eqright = ITEM_HATCHET
    self.eflags = ENCHANT_FLAG_SUMMON_1
  if(subtype == 1)
    if(RollDice(1, 20)&1)
      self.eqleft = ITEM_LONG_BOW
      self.eqright = ITEM_SWORD
      self.item00 = ITEM_QUIVER_10
      self.item01 = ITEM_QUIVER_10
      self.item02 = ITEM_QUIVER_10
    else
      self.eqleft = ITEM_MIRROR_SHIELD
      if(RollDice(1, 20)&1)
        self.eqright = ITEM_GREAT_SWORD
      else
        self.eqright = ITEM_BROAD_AXE
    self.eflags = ENCHANT_FLAG_SUMMON_1
  if(subtype == 2)
    if(RollDice(1, 20)&1)
      self.eqright = ITEM_DRAGON_SWORD
    else
      self.eqleft = ITEM_MIRROR_SHIELD
      self.eqright = ITEM_MACE
    self.eqspec2 = ITEM_SPELL_FIREBALL
    self.dex = 20
    self.int = 30
    self.manamax = 10
    self.oflags = self.oflags | OTHER_FLAG_NO_MORPH
    self.eflags = ENCHANT_FLAG_SUMMON_1 | ENCHANT_FLAG_DEFLECT
  if(subtype == 3)
    self.boxsize = self.boxsize*LICH_SCALE
    self.height = self.height*LICH_SCALE
    self.timer = 1
    self.dex = 20
    self.int = 50
    self.manamax = 30
    self.oflags = self.oflags | OTHER_FLAG_NO_MORPH
    self.eflags = ENCHANT_FLAG_DEFLECT
  self.mana = self.manamax


  STANDARD.FixStats(self)
  ModelSetup()


//-----------------------------------------------------------------------
