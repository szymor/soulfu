//-----------------------------------------------------------------------
StopMusic()
  PlayMusic(NULL, 0, MUSIC_STOP)

//-----------------------------------------------------------------------
int PlayCenterSound(int file, int frequency, int volume)
  PlayMegaSound(file, frequency, volume, 128, NULL)

//-----------------------------------------------------------------------
// Function is called by a character to remove it from a
// map room's monster table (without poofing the monster)
int MapObjectDefeated()
  int index
  index = FindIndex(FindSelf())
  SystemSet(SYS_MAPOBJECTDEFEATED, 0, 0, index)

//-----------------------------------------------------------------------
int RestoreCharacterHits(int target, int restore, int untap)
  int temp, max, original, child
  if(target)
    // First do the untapping...
    original = target.hits
    temp = target.hitstap - untap
    if(temp < 0)
      temp = 0
    target.hitstap = temp


    // Then do the restoring...
    temp = target.hits+restore
    max = target.hitsmax-target.hitstap
    if(temp > max)
      temp = max
    target.hits = temp


    // Now spawn a heal number particle...
    temp = temp - original
    if(temp > 0)
      child = Spawn(PARTICLE, target.x, target.y, target.z + (target.height - 1), "FILE:PNUMBER.RUN") 
      if(child)
        child.number = temp


//-----------------------------------------------------------------------
int RestoreCharacterMana(int target, int restore, int untap)
  int temp, max
  if(target)
    // First do the untapping...
    temp = target.manatap - untap
    if(temp < 0)
      temp = 0
    target.manatap = temp


    // Then do the restoring...
    temp = target.mana+restore
    max = target.manamax-target.manatap
    if(temp > max)
      temp = max
    target.mana = temp

//-----------------------------------------------------------------------
// Returns the .RUN file for a given character...
int CharacterScriptFile(int index)
  return SystemGet(SYS_CHARACTERSCRIPTFILE, 0, index)

//-----------------------------------------------------------------------
// Function returns the maximum number of helpers a
// character can spawn...  If self is NULL or a local
// player or one of the local pets, the total number
// of local pets allowed is returned.  Otherwise, the
// monster_override number is returned...
int MaxHelpers(int self, int monster_override)
  int index, target, number, player

  index = FindIndex(self)
  if(self)
    if(!IndexIsLocalPlayer(index) && !IndexIsLocalPlayer(self.owner))
      if(FileReadByte(self, 252) == 0 && FileReadByte(self, 253) == 0 && FileReadByte(self, 254) == 0 && FileReadByte(self, 255) == 0)
        return monster_override
      // Network monsters shouldn't spawn more...
      return 0


  if(SystemGet(SYS_PAYINGCUSTOMER, 0, 0) == FALSE)
    return 1


  // If we got here, it means we need to count all the local players...
  number = 0
  player = 0
  while(player < 4)
    target = FindByIndex(SystemGet(SYS_LOCALPLAYER, player, 0))
    if(target)
      // Players with badge of compassion are limited to no helpers (must keep existing ones alive)
      if(target.vircomp != 255)
        number = number + (target.int/10)
        if(target.class == CLASS_SOLDIER)
          number++
    player++
  return number


//-----------------------------------------------------------------------
int NumLocalPlayer()
  int number, player
  number = 0
  player = 0
  while(player < 4)
    if(SystemGet(SYS_PLAYERDEVICE, player, PLAYER_DEVICE_TYPE)!=PLAYER_DEVICE_NONE)
      number++
    player++
  return number


//-----------------------------------------------------------------------
int NumLocalPlayerActive()
  int number, player
  number = 0
  player = 0
  while(player < 4)
    if(FindByIndex(SystemGet(SYS_LOCALPLAYER, player, 0)))
      number++
    player++
  return number


//-----------------------------------------------------------------------
// Function returns number of pets owned by the local
// players...
int NumLocalHelpers()
  int i, count, target
  i = 0
  count = 0
  while(i < MAX_CHARACTER)
    target = FindByIndex(i)
    if(target)
      if(IndexIsLocalPlayer(target.owner))
        count++
    i++
  return count

//-----------------------------------------------------------------------
// Function returns the number of characters in a room that
// have the current character listed as their owner...  If
// self is a local player or one of the local player's
// helpers, all of the local pets are counted...
int ChildCount(int self)
  int index, i, target, count, owner
  count = 0
  if(self)
    index = FindIndex(self)
    if(IndexIsLocalPlayer(index) || IndexIsLocalPlayer(self.owner))
      count = NumLocalHelpers()
    else
      owner = FindByIndex(self.owner)
      if(owner)
        owner = self.owner
      else
        owner = -1
      i = 0
      while(i < MAX_CHARACTER)
        target = FindByIndex(i)
        if(target)
          if(target.owner == index || target.owner == owner)
            count++
        i++
  return count

//-----------------------------------------------------------------------
// Function returns TRUE if the target and target_item are both valid
// for a given character.  Used to make sure enchantments are targeting
// a valid inventory item...
int EnchantItemCheck(int target, int target_item)
  int item
  if(target && target_item > 223 && target_item < 249 && target_item != 240 && target_item != 241)
    item = FileReadByte(target, target_item)
    if(item)
      return TRUE
  return FALSE

//-----------------------------------------------------------------------
// Function jiggles a character around until it isn't stuck in a
// wall...
int FixCharacterLocation(int self)
  float angle, testx, testy, testz, distance

  if(self)
    testz = AreaHeightXY(self.x, self.y, self.boxsize)
    if(testz < (self.z+2.0))
      self.z = testz
      return TRUE

    distance = 2.0
    while(distance < 10.0)
      angle = 0.0
      while(angle < 360.0)
        testx = self.x + sin(angle)*distance
        testy = self.y + sin(angle+90.0)*distance
        testz = AreaHeightXY(testx, testy, self.boxsize)
        if(testz < (self.z+2.0))
          self.x = testx
          self.y = testy
          self.z = testz
          return TRUE
        angle = angle+45.0
      distance = distance+2.0
    return TRUE
  return FALSE

//-----------------------------------------------------------------------
// Function returns z value of room, with an area check thing...
float AreaHeightXY(float x, float y, float boxsize)
  float high, angle, testx, testy, testz


  boxsize = boxsize + 0.25


  // Check for walls and pits...
  high = RoomHeightXY(x, y)
  angle = 0.0
  while(angle < 360.0)
    testx = x + sin(angle)*boxsize
    testy = y + sin(angle+90.0)*boxsize
    testz = RoomHeightXY(testx, testy)
    if(testz > high)
      high = testz
    angle = angle + 27.5
  return high



//-----------------------------------------------------------------------
// <ZZ> This function returns TRUE if the desired character location is
/       good, FALSE if it's bad...  Checks for wall collisions and door
//      collisions...
int TestLocation(float x, float y, float z, float boxsize)
  int i, target
  float high, angle, testx, testy, testz

  boxsize = boxsize + 0.25


  // Check for walls and pits...
  high = RoomHeightXY(x, y)
  angle = 0.0
  while(angle < 360.0)
    testx = x + sin(angle)*boxsize
    testy = y + sin(angle+90.0)*boxsize
    testz = RoomHeightXY(testx, testy)
    if(testz > high)
      high = testz
    angle = angle + 27.5
  if(high > (z+4.0) || high < (z-4.0))
    return FALSE



  // Check for behind doors...
  i = 0
  while(i < MAX_CHARACTER)
    target = FindByIndex(i)
    if(target)
      if(target.mflags & MORE_FLAG_DOOR)
        testx = x - target.x
        testy = y - target.y
        if((testx*target.frontx + testy*target.fronty) < 0.0)
          testx = testx*testx
          testy = testy*testy
          testz = testz*testz
          testz = boxsize+target.boxsize
          if((testx+testy) < testz)
            return FALSE
    i++
  return TRUE


//-----------------------------------------------------------------------
// Function sets up the target character's rotation matrix...
// May need to call if character is spawned from a character/particle
// script...
int SetupTargetMatrix(int target)
  int angle
  float sine, cosine, x


  x = 1.0
  angle = target.spin*0.0054931640625
  sine = sin(angle)
  cosine = sin(angle+90.0)
  if(target.flatten < 255)
    x = ((255-target.flatten)*0.001953125) + 1.0
    sine = sine*x
    cosine = cosine*x
    x = target.flatten * 0.00390625

  target.sidex = sine
  target.sidey = -cosine
  target.sidez = 0.0

  target.frontx = cosine
  target.fronty = sine
  target.frontz = 0.0

  target.upx = 0.0
  target.upy = 0.0
  target.upz = x

//-----------------------------------------------------------------------
// Function returns TRUE if any of the four local players is
// of the specified class...
int LocalPlayerIsClass(int class)
  int target

  target = FindByIndex(SystemGet(SYS_LOCALPLAYER, 0, 0))
  if(target)
    if(target.class == class)
      return TRUE
  target = FindByIndex(SystemGet(SYS_LOCALPLAYER, 1, 0))
  if(target)
    if(target.class == class)
      return TRUE
  target = FindByIndex(SystemGet(SYS_LOCALPLAYER, 2, 0))
  if(target)
    if(target.class == class)
      return TRUE
  target = FindByIndex(SystemGet(SYS_LOCALPLAYER, 3, 0))
  if(target)
    if(target.class == class)
      return TRUE
  return FALSE

//-----------------------------------------------------------------------
// Function returns TRUE if view is clear from character to
// the given xyz point.  Should only be called by a character...
int LineOfSight(float endx, float endy, float endz)
  float x, y, z
  float stepx, stepy, stepz, height
  int i, self

  self = FindSelf()
  x = self.x
  y = self.y
  z = self.z + (self.height*0.5)
  stepx = (endx-x)*0.1
  stepy = (endy-y)*0.1
  stepz = (endz-z)*0.1

  i = 0
  while(i < 9)
    x = x+stepx
    y = y+stepy
    z = z+stepz
    height = RoomHeightXY(x, y)
    if(height > z)
      // View is blocked...
      return FALSE
    i++
  // Didn't hit any obstructions...
  return TRUE

//-----------------------------------------------------------------------
int RoomWaterType()
  return (SystemGet(SYS_ROOMWATERTYPE, 0, 0))

//-----------------------------------------------------------------------
float RoomWaterLevel()
  return (SystemGet(SYS_ROOMWATERLEVEL, 0, 0)*0.01)

//-----------------------------------------------------------------------
SetSpawnTarget(int target_index)
  SystemSet(SYS_GLOBALSPAWN, 0, GLOBAL_SPAWN_TARGET, target_index)

//-----------------------------------------------------------------------
SetSpawnOwner(int owner_index)
  SystemSet(SYS_GLOBALSPAWN, 0, GLOBAL_SPAWN_OWNER, owner_index)

//-----------------------------------------------------------------------
SetSpawnTeam(int team)
  SystemSet(SYS_GLOBALSPAWN, 0, GLOBAL_SPAWN_TEAM, team)

//-----------------------------------------------------------------------
SetSpawnSubtype(int subtype)
  SystemSet(SYS_GLOBALSPAWN, 0, GLOBAL_SPAWN_SUBTYPE, subtype)

//-----------------------------------------------------------------------
int MagicWillWork(int manamax, int mana, int mana_required)
  if(manamax > 0 && mana >= mana_required && (mana > 0 || mana_required == 0))
    return TRUE
  return FALSE

//-----------------------------------------------------------------------
int ButtonHandled()
  ButtonPress(BUTTON_HANDLED, 0, 0)

//-----------------------------------------------------------------------
int CenterString(int color, float x, float y, int text)
  WindowString(color, x-(StringLength(text)*0.5), y, text)

//-----------------------------------------------------------------------
int WindowYesBox(int value, float x, float y)
  int click

  if(value)
    click = WindowImage(x, y, 1.0, 1.0, "FILE:WINYABOX.RGB", NULL, 0)
    if(click == 255)
      ButtonSound()
      return 0
    return 1
  else
    click = WindowImage(x, y, 1.0, 1.0, "FILE:WINNOBOX.RGB", NULL, 0)
    if(click == 255)
      ButtonSound()
      return 1
    return 0

//-----------------------------------------------------------------------
int CharacterHasItem(int target, int item, int remove_it)
  // Character is a pointer, not an index...  Returns TRUE if it character
  // has it...  Removes it if remove_it is set...  Ammo & Quivers
  // lose just one of their count...
  int slot, low, high, index


  // Look for it...
  low = item
  high = item
  if(item == ITEM_QUIVER_10)
    high = ITEM_QUIVER_1
  if(item == ITEM_AMMO_10)
    high = ITEM_AMMO_1
  slot = 224
  while(slot < 248)
    if(slot != 240 && slot != 241)
      item = FileReadByte(target, slot)
      if(item >= low && item <= high)
        if(remove_it)
          if(low != high)
            // Quiver or Ammo
            if(item == high)
              // Last one...
              FileWriteByte(target, slot, ITEM_NONE)
            else
              FileWriteByte(target, slot, item+1)
              return TRUE  // No need to update model...
          else
            // Normal item
            FileWriteByte(target, slot, ITEM_NONE)
        if(slot > 241)
          // Make sure the character looks right (since we removed an equip'd item)
          index = FindIndex(target)
          SystemSet(SYS_CHARFASTFUNCTION, index, 0, FAST_FUNCTION_MODELSETUP)
          SystemSet(SYS_CHARFASTFUNCTION, index, 0, FAST_FUNCTION_DEFENSERATING)
        return TRUE
    slot++
  return FALSE

//-----------------------------------------------------------------------
int GiveItemToCharacter(int target, int item)
  // Character is a pointer, not an index...  Returns TRUE if it worked...
  // Should then spawn PITEM manually to show what it was...
  int slot, old_item, num_slot


  // Look for an empty spot in inventory...
  slot = 0
  num_slot = 6
  if(IndexIsLocalPlayer(FindIndex(target)))
    num_slot = 16
  while(slot < num_slot)
    old_item = FileReadByte(target, slot+224)
    if(old_item == ITEM_NONE)
      FileWriteByte(target, slot+224, item)
      return TRUE
    slot++
  // No empty slots...
  return FALSE

//-----------------------------------------------------------------------
// Appends the damage amounts to an item's name...
int ItemNameDamage(int damage_type, int damage_low, int damage_high, int wound_low, int wound_high)
  StringAppend(NAME_STRING, " (", 256)
  if(damage_low > 0 || damage_high > 0)
    if(damage_type == DAMAGE_EDGE)
      StringAppend(NAME_STRING, "EDGE: ", 256)
    if(damage_type == DAMAGE_BASH)
      StringAppend(NAME_STRING, "BASH: ", 256)
    if(damage_type == DAMAGE_ACID)
      StringAppend(NAME_STRING, "ACID: ", 256)
    if(damage_type == DAMAGE_FIRE)
      StringAppend(NAME_STRING, "FIRE: ", 256)
    if(damage_type == DAMAGE_ICE)
      StringAppend(NAME_STRING, "ICE: ", 256)
    if(damage_type == DAMAGE_VOLT)
      StringAppend(NAME_STRING, "VOLT: ", 256)
    if(damage_type == DAMAGE_WOUND)
      StringAppend(NAME_STRING, "WOUND: ", 256)
    if(damage_low == damage_high)
      StringAppendNumber(NAME_STRING, damage_low, 256)
    else
      StringAppendNumber(NAME_STRING, damage_low, 256)
      StringAppend(NAME_STRING, "-", 256)
      StringAppendNumber(NAME_STRING, damage_high, 256)
  if(wound_low > 0 || wound_high > 0)
    if(damage_low > 0 || damage_high > 0)
      StringAppend(NAME_STRING, ", ", 256)
    StringAppend(NAME_STRING, "WOUND: ", 256)
    if(wound_low == wound_high)
      StringAppendNumber(NAME_STRING, wound_low, 256)
    else
      StringAppendNumber(NAME_STRING, wound_low, 256)
      StringAppend(NAME_STRING, "-", 256)
      StringAppendNumber(NAME_STRING, wound_high, 256)
  StringAppend(NAME_STRING, ")", 256)


//-----------------------------------------------------------------------
// Gives back an RGB color for any of 16 values...
int HairColor(int value)
  if(value & 8)
    if(value & 4)
      if(value & 2)
        if(value & 1)
          // 15
          return WHITE
        else
          // 14
          return 8421504
      else
        if(value & 1)
          // 13
          return 13372672
        else
          // 12
          return 14238208
    else
      if(value & 2)
        if(value & 1)
          // 11
          return 14971392
        else
          // 10
          return 15836928
      else
        if(value & 1)
          // 9
          return 16769280
        else
          // 8
          return 14334212
  else
    if(value & 4)
      if(value & 2)
        if(value & 1)
          // 7
          return 12162568
        else
          // 6
          return 10583323
      else
        if(value & 1)
          // 5
          return 10056459
        else
          // 4
          return 8411679
    else
      if(value & 2)
        if(value & 1)
          // 3
          return 7818767
        else
          // 2
          return 6900271
      else
        if(value & 1)
          // 1
          return 6113100
        else
          // 0
          return 2105415

//-----------------------------------------------------------------------
// Gives back an RGB color for any of 16 values...
int RGBColor(int value)
  if(value & 8)
    if(value & 4)
      if(value & 2)
        if(value & 1)
          // 15
          return 4634015
        else
          // 14
          return 11880518
      else
        if(value & 1)
          // 13
          return 5420614
        else
          // 12
          return 4620213
    else
      if(value & 2)
        if(value & 1)
          // 11
          return 8537781
        else
          // 10
          return 12496795
      else
        if(value & 1)
          // 9
          return 15650390
        else
          // 8
          return 14503502
  else
    if(value & 4)
      if(value & 2)
        if(value & 1)
          // 7
          return 11095773
        else
          // 6
          return 14520654
      else
        if(value & 1)
          // 5
          return 14503593
        else
          // 4
          return 12246606
    else
      if(value & 2)
        if(value & 1)
          // 3
          return 5168590
        else
          // 2
          return 5153501
      else
        if(value & 1)
          // 1
          return 544124
        else
          // 0
          return 16708324

//-----------------------------------------------------------------------
// Gives back a color file for any of 16 values...
int DullColor(int value)
  if(value & 8)
    if(value & 4)
      if(value & 2)
        if(value & 1)
          // 15
          return "FILE:DCYAN.RGB"
        else
          // 14
          return "FILE:DRED.RGB"
      else
        if(value & 1)
          // 13
          return "FILE:DGREEN.RGB"
        else
          // 12
          return "FILE:DBLUE.RGB"
    else
      if(value & 2)
        if(value & 1)
          // 11
          return "FILE:DPURPLE.RGB"
        else
          // 10
          return "FILE:DWHITE.RGB"
      else
        if(value & 1)
          // 9
          return "FILE:YELLOW.RGB"
        else
          // 8
          return "FILE:RED.RGB"
  else
    if(value & 4)
      if(value & 2)
        if(value & 1)
          // 7
          return "FILE:PURPLE.RGB"
        else
          // 6
          return "FILE:ORANGE.RGB"
      else
        if(value & 1)
          // 5
          return "FILE:MAGENTA.RGB"
        else
          // 4
          return "FILE:GREEN.RGB"
    else
      if(value & 2)
        if(value & 1)
          // 3
          return "FILE:CYAN.RGB"
        else
          // 2
          return "FILE:BLUE.RGB"
      else
        if(value & 1)
          // 1
          return "FILE:BLACK.RGB"
        else
          // 0
          return "FILE:WHITE.RGB"

//-----------------------------------------------------------------------
// Gives back a glossy color file for any of 16 values...
int GlossColor(int value)
  if(value & 8)
    if(value & 4)
      if(value & 2)
        if(value & 1)
          // 15
          return "FILE:NCYAN.RGB"
        else
          // 14
          return "FILE:NRED.RGB"
      else
        if(value & 1)
          // 13
          return "FILE:NGREEN.RGB"
        else
          // 12
          return "FILE:NBLUE.RGB"
    else
      if(value & 2)
        if(value & 1)
          // 11
          return "FILE:NPURPLE.RGB"
        else
          // 10
          return "FILE:NWHITE.RGB"
      else
        if(value & 1)
          // 9
          return "FILE:GYELLOW.RGB"
        else
          // 8
          return "FILE:GRED.RGB"
  else
    if(value & 4)
      if(value & 2)
        if(value & 1)
          // 7
          return "FILE:GPURPLE.RGB"
        else
          // 6
          return "FILE:GORANGE.RGB"
      else
        if(value & 1)
          // 5
          return "FILE:GMAGENTA.RGB"
        else
          // 4
          return "FILE:GGREEN.RGB"
    else
      if(value & 2)
        if(value & 1)
          // 3
          return "FILE:GCYAN.RGB"
        else
          // 2
          return "FILE:GBLUE.RGB"
      else
        if(value & 1)
          // 1
          return "FILE:GBLACK.RGB"
        else
          // 0
          return "FILE:GWHITE.RGB"


//-----------------------------------------------------------------------
// For debug stuff...
int LogNumbers(int message, int x, int y, int z)
  StringClear(DEBUG_STRING)
  StringAppend(DEBUG_STRING, message, 256)
  StringAppend(DEBUG_STRING, ", ", 256)
  StringAppendNumber(DEBUG_STRING, x, 256)
  StringAppend(DEBUG_STRING, ", ", 256)
  StringAppendNumber(DEBUG_STRING, y, 256)
  StringAppend(DEBUG_STRING, ", ", 256)
  StringAppendNumber(DEBUG_STRING, z, 256)
  LogMessage(DEBUG_STRING)
  StringClear(DEBUG_STRING)

//-----------------------------------------------------------------------
// Given the actual checksum, returns the alphanumeric value we want...
int GetCheckSum(int checksum)
  checksum = checksum & 63
  if(checksum < 10)
    return (checksum+'0')
  checksum = checksum - 10
  if(checksum < 26)
    return (checksum+'a')
  checksum = checksum - 26
  if(checksum < 26)
    return (checksum+'A')
  if(checksum == 0)
    return 47
  return 92

//-----------------------------------------------------------------------
int FootSound(int ground_type)
  int self, volume, file, pitch

  self = FindSelf()
  volume = RollDice(1, 64) + 64
  pitch = RollDice(1, 64) + 224
  file = RollDice(1, 3)-1
  if(ground_type == 0)
    if(file == 0)
      PlaySound("FILE:FTHARD00.RAW", pitch, volume)
    if(file == 1)
      PlaySound("FILE:FTHARD01.RAW", pitch, volume)
    if(file == 2)
      PlaySound("FILE:FTHARD02.RAW", pitch, volume)
  if(ground_type == 1)
    if(file == 0)
      PlaySound("FILE:FTGRAS00.RAW", pitch, volume)
    if(file == 1)
      PlaySound("FILE:FTGRAS01.RAW", pitch, volume)
    if(file == 2)
      PlaySound("FILE:FTGRAS02.RAW", pitch, volume)
  if(ground_type == 2)
    if(file == 0)
      PlaySound("FILE:FTSOFT00.RAW", pitch, volume)
    if(file == 1)
      PlaySound("FILE:FTSOFT01.RAW", pitch, volume)
    if(file == 2)
      PlaySound("FILE:FTSOFT02.RAW", pitch, volume)


//-----------------------------------------------------------------------
// Returns TRUE if the filename string has the given extension...  ("TEST.TXT", "TXT")...
int CheckExtension(int filename, int extension)
  int length

  length = StringLength(filename)
  StringUppercase(filename)
  if(StringGetValue(filename, length-3) == StringGetValue(extension, 0))
    if(StringGetValue(filename, length-2) == StringGetValue(extension, 1))
      if(StringGetValue(filename, length-1) == StringGetValue(extension, 2))
        return TRUE
  return FALSE


//-----------------------------------------------------------------------
// Draws a titlebar for a window...
int TitleBar(int drag, float x, float y, int title)
  int length
  length = StringLength(title)
  WindowBorder(drag, x-(length*0.5), y-1.7, length, 1, BORDER_TITLE)
  WindowString(WHITE, 0.0, -0.1, title)


//-----------------------------------------------------------------------
// Draws a full titlebar for a window...
int TitleFull(int drag, float x, float y, int title)
  int length
  length = StringLength(title)
  WindowBorder(drag, x-(length*0.5), y-0.5, length, 1, BORDER_FULL)
  WindowString(WHITE, 0.0, 0.0, title)


//-----------------------------------------------------------------------
// Plays the click sound for a button
int ButtonSound()
  PlaySound("FILE:-BUTTON.RAW", 256, 255)
  return TRUE


//-----------------------------------------------------------------------
// Plays the sound for a buzzer
int BuzzerSound()
  PlaySound("FILE:-BUZZER.RAW", 256, 128)
  return TRUE


//-----------------------------------------------------------------------
// Draws a button...  Handles clicking...
int ButtonImage(int value, float x, float y, float w, float h, int tag)
  float length
  length = StringLength(tag)
  if(value < 250)
    value = WindowImage(x, y, w, h, "FILE:BUTTON.RGB", NULL, 0)
    WindowString(WHITE, x+(w-length)*0.5, y+((h-1.0)*0.5), tag)
  else
    WindowImage(x+w, y+h, -w, -h, "FILE:BUTTON.RGB", NULL, 0)
    WindowString(WHITE, x+((w-length)*0.5)+0.2, y+((h-0.6)*0.5), tag)
    value--
  return value


//-----------------------------------------------------------------------
// Draws an up/down button...  Handles clicking...
int ButtonImageVertical(int value, float x, float y, float w, float h, int tag)
  float length
  int i, size
  int olda, oldb


  // Corrupt String(0)...
  olda = StringGetValue(String(0), 0)
  oldb = StringGetValue(String(0), 1)
  StringSetValue(String(0), 1, 0)


  size = StringLength(tag)
  length = size
  if(value < 250)
    WindowMegaImage(x+w, y, x+w, y+h, x, y+h, x, y, 0.0, 0.0, 1.0, 1.0, WHITE, ALPHA_NONE, "FILE:BUTTON.RGB")
    value = WindowImage(x, y, w, h, NULL, NULL, 0)
    i = 0
    while(i < size)
      StringSetValue(String(0), 0, StringGetValue(tag, i))
      WindowString(WHITE, x+(w-1.0)*0.5, y+i+((h-length)*0.5), String(0))
      i++
  else
    WindowMegaImage(x+w, y, x+w, y+h, x, y+h, x, y, 1.0, 1.0, 0.0, 0.0, WHITE, ALPHA_NONE, "FILE:BUTTON.RGB")
    WindowImage(x, y, w, h, NULL, NULL, 0)  // So it doesn't change current item
    value--
    i = 0
    while(i < size)
      StringSetValue(String(0), 0, StringGetValue(tag, i))
      WindowString(WHITE, x+(w-1.0)*0.5+0.2, y+i+0.2+((h-length)*0.5), String(0))
      i++


  // Uncorrupt String(0)
  StringSetValue(String(0), 0, olda)
  StringSetValue(String(0), 1, oldb)
  return value


//-----------------------------------------------------------------------
ModelClear()
  int self
  self = FindSelf()
  ModelAssign(self+MODEL_BASE_FILE, NULL)
  ModelAssign(self+MODEL_LEGS_FILE, NULL)
  ModelAssign(self+MODEL_CHEST_FILE, NULL)
  ModelAssign(self+MODEL_ARMS_FILE, NULL)
  ModelAssign(self+MODEL_PADS_FILE, NULL)
  ModelAssign(self+MODEL_FEET_FILE, NULL)
  ModelAssign(self+MODEL_HEAD_FILE, NULL)
  ModelAssign(self+MODEL_HAIR_FILE, NULL)
  ModelAssign(self+MODEL_EYES_FILE, NULL)
  ModelAssign(self+MODEL_MOUTH_FILE, NULL)
  ModelAssign(self+MODEL_LEFT_FILE, NULL)
  ModelAssign(self+MODEL_RIGHT_FILE, NULL)
  ModelAssign(self+MODEL_LEFT2_FILE, NULL)
  ModelAssign(self+MODEL_RIGHT2_FILE, NULL)


//-----------------------------------------------------------------------
