#define CHEAT FALSE // (SystemGet(SYS_DEVTOOL, 0, 0) && SystemGet(SYS_KEYDOWN, 99, 0))

#define gem           self.stat00l
#define icon1         self.stat00h
#define icon2         self.stat01l
#define icon3         self.stat01h
#define icon4         self.stat02l
#define icon5         self.stat02h
#define chat_player   self.stat03l
#define chat_shown    self.stat03h
#define timer         self.stat04w
#define chat_message  self.bigtext


#define RESTOCK_DELAY 1000


#define KEYB      98
#define KEYC      99
#define KEYG      103
#define KEYK      107
#define KEYL      108
#define KEYM      109
#define KEYP      112
#define KEYR      114
#define KEYT      116
#define KEYW      119
#define KEYZ      122
#define KEY_F1  282
#define KEY_F2  283
#define KEY_F3  284
#define KEY_F4  285
#define KEY_EQUALS 61


//-----------------------------------------------------------------------
Spawn()
  int    self
  self = FindSelf()
  self.y = -999.0
  icon1 = 0
  icon2 = 0
  icon3 = 0
  icon4 = 0
  icon5 = 0
  chat_player = 255
  chat_shown = FALSE
  gem = FALSE
  timer = 0

  StringClear(chat_message)


//-----------------------------------------------------------------------
Refresh()
  int    self, chat_value, target, length, c, child, subwin, player
  int    sizex, titlesize, chatsize, chatlength, chatoffset, makeactive
  int    old_input_active, append_all, frame_skip, alpha
  int    i, j, type, spin, count, subtype
  float  max, x, curx, cury, spawnx, spawny
  float  offx, screenx



  // Allow us to save data in the window's properties...
  self = FindSelf()
  max = (SystemGet(SYS_WINDOWSCALE, 0, 0) * 0.00125) + 4.5
  offx = ((SystemGet(SYS_WINDOWSCALE, 0, 0) * 0.00166666666) + 6.0)
  screenx = SystemGet(SYS_SCREENPARAMS, 0, 2)
  sizex = screenx / offx
  sizex--
  offx = (screenx - (sizex * offx)) * 0.5
  SystemSet(SYS_MESSAGESIZE, 0, 0, sizex)




  // Restock monsters...
  timer = timer + SystemGet(SYS_MAINFRAMESKIP, 0, 0)
  if(timer > RESTOCK_DELAY)
    i = MAPGEN.GetNumberOfRooms()
    if(i > 0)
      j = ((RollDice(1, 256)-1)<<8) | (RollDice(1, 256)-1)
      i = j % i
      SystemSet(SYS_ROOMRESTOCK, 0, 0, i)
//      StringClear(DEBUG_STRING)
//      StringAppend(DEBUG_STRING, "Restock ", 256)
//      StringAppendNumber(DEBUG_STRING, i, 256)
//      DebugMessage(DEBUG_STRING)
//      StringClear(DEBUG_STRING)
    timer = timer - RESTOCK_DELAY




  // Check for chat start...
  makeactive = FALSE
  append_all = FALSE
  old_input_active = SystemGet(SYS_INPUTACTIVE, 0, 0)
  SystemSet(SYS_INPUTACTIVE, 0, 0, 0)
//  if(SystemGet(SYS_KEYPRESSED, KEY_F1, 0))
//    if(FindByIndex(SystemGet(SYS_LOCALPLAYER, 0, 0)))
//      if(chat_player==0)
//        append_all = TRUE
//      chat_player = 0
//      makeactive = TRUE
//  if(SystemGet(SYS_KEYPRESSED, KEY_F2, 0))
//    if(FindByIndex(SystemGet(SYS_LOCALPLAYER, 1, 0)))
//      if(chat_player==1)
//        append_all = TRUE
//      chat_player = 1
//      makeactive = TRUE
//  if(SystemGet(SYS_KEYPRESSED, KEY_F3, 0))
//    if(FindByIndex(SystemGet(SYS_LOCALPLAYER, 2, 0)))
//      if(chat_player==2)
//        append_all = TRUE
//      chat_player = 2
//      makeactive = TRUE
//  if(SystemGet(SYS_KEYPRESSED, KEY_F4, 0))
//    if(FindByIndex(SystemGet(SYS_LOCALPLAYER, 3, 0)))
//      if(chat_player==3)
//        append_all = TRUE
//      chat_player = 3
//      makeactive = TRUE
  SystemSet(SYS_INPUTACTIVE, 0, 0, old_input_active)
  if(append_all && old_input_active)
    // Key was hit twice, so append the <ALL> tag to the start of the string...
    if(StringLength(chat_message) == 0)
      StringAppend(chat_message, "<ALL> ", 128)
      SystemSet(SYS_LASTINPUTCURSORPOS, 0, 0, 7)



  // Find the chat player's character...
  player = NULL
  if(chat_player < 4)
    // Search for a valid player
    player = SystemGet(SYS_LOCALPLAYER, chat_player, 0)
    player = FindByIndex(player)



  // The player character was found...
  if(player)
    if(chat_shown == FALSE)
      // Chat message typer-inner wasn't active last update, so
      // we need to clean out the input buffer (so we don't get
      // keys that were pressed before hitting F1-F4)
      SystemSet(SYS_MAKEINPUTACTIVE, 0, 0, 0)


    // Custom player-name title bar
    StringClear(String(0))
    StringAppend(String(0), player.name, 16)
    titlesize = StringLength(String(0))
    WindowBorder(FALSE, 0.0, 5.7, titlesize,  1, BORDER_LOW_TITLE)
    WindowString(WHITE, 0.0, 0.1, String(0))


    chatsize = sizex-titlesize-1
    chatlength = StringLength(chat_message)
    chatoffset = chatlength - (chatsize>>1)
    if(chatoffset < 0)
      chatoffset = 0
    if((chatoffset + chatsize) > 120)
      chatoffset = 120 - chatsize


    WindowBorder(FALSE, titlesize+1, 5.7, chatsize,  1, 29680)
    chat_value = WindowInput(0.0, 0.25, chatsize, chat_message+chatoffset, 0)
    if(makeactive)
      SystemSet(SYS_MAKEINPUTACTIVE, 0, 0, 0)
    chat_shown = TRUE
    if(chat_value > 0 && chat_value < 255)
      chatlength = StringLength(chat_message)
      chatoffset = chatlength - (chatsize>>1)
      if(chatoffset < 0)
        chatoffset = 0
      if((chatoffset + chatsize) > 120)
        chatoffset = 120 - chatsize
      SystemSet(SYS_LASTINPUTCURSORPOS, 0, 0, chatlength-chatoffset)


    if(chat_value == 255)
      // !!!BAD!!!
      // !!!BAD!!!
      // !!!BAD!!!
      // Parse out escape codes...
      //   <ALL>   Message goes to all in game...
      //   <NAME>  Click on a target to give it the typed name...
      //   <TALK>  Talk item...
      // !!!BAD!!!
      // !!!BAD!!!
      // !!!BAD!!!
      NetworkMessage(player.name, chat_message, player.class)
      StringClear(chat_message)
      SystemSet(SYS_LASTINPUTCURSORPOS, 0, 0, 0)
      chat_player = 255
  else
    // Turn off the chat bar..
    chat_player = 255
    chat_shown = FALSE


    // Title bar
    titlesize = StringLength(StringLanguage(5))
    WindowBorder(FALSE, 0.0, 5.7, titlesize,  1, BORDER_LOW_TITLE)
    WindowString(WHITE, 0.0, 0.1, StringLanguage(5))


    // Gemstone status info...
    if(gem)
      // Players bar...
      StringClear(String(0))
      StringAppendNumber(String(0), SystemGet(SYS_NUMNETWORKPLAYER, 0, 0), 256)
      StringAppend(String(0), StringLanguage(232), 256)
      length = StringLength(String(0))
      x = sizex-length
      WindowBorder(FALSE, x, 5.7, length,  1, BORDER_LOW_TITLE)
      WindowString(WHITE, 0.0, 0.1, String(0))


      // FPS bar
      StringClear(String(0))
      length = SystemGet(SYS_FPS, 0, 0)
      if(length > 99)
        length = 99
      StringAppendNumber(String(0), length, 256)
      StringAppend(String(0), " FPS", 256)
      if(length < 10)
        length = 5
      else
        length = 6
      x = x-7.25
      WindowBorder(FALSE, x, 5.7, 6,  1, BORDER_LOW_TITLE)
      WindowString(WHITE, (6-length), 0.1, String(0))



      StringClear(String(0))
      length = NumLocalHelpers()
      StringAppendNumber(String(0), length, 256)
      StringAppend(String(0), "/", 256)
      length = MaxHelpers(NULL, 0)
      StringAppendNumber(String(0), length, 256)
      StringAppend(String(0), " Helpers", 256)
      length = StringLength(String(0))
      x = x-(length+1)
      WindowBorder(FALSE, x, 5.7, length,  1, BORDER_LOW_TITLE)
      WindowString(WHITE, 0, 0.1, String(0))



  // Message window
  WindowBorder(FALSE, 0.0, 0.0, sizex,  5, 65534)  // BORDER_FULL without bl corner...


  // Gemstone toggle...
  if(gem)
    c = WindowImage(-0.6, 4.6, 1.0, 1.0, "FILE:=WINGEMG.RGB", NULL, 0)
  else
    c = WindowImage(-0.6, 4.6, 1.0, 1.0, "FILE:=WINGEMR.RGB", NULL, 0)
  if(c == 255) 
    gem = (gem+1)&1
    ButtonSound()


  if(self.y > max*-1.00)
    WindowString(WHITE, 0.0, 0.0, SystemGet(SYS_MESSAGE, 4, 0))
  if(self.y > max*-2.40)
    WindowString(WHITE, 0.0, 1.0, SystemGet(SYS_MESSAGE, 3, 0))
  if(self.y > max*-3.80)
    WindowString(WHITE, 0.0, 2.0, SystemGet(SYS_MESSAGE, 2, 0))
  if(self.y > max*-5.20)
    WindowString(WHITE, 0.0, 3.0, SystemGet(SYS_MESSAGE, 1, 0))
  if(self.y > max*-6.60)
    WindowString(WHITE, 0.0, 4.0, SystemGet(SYS_MESSAGE, 0, 0))





  // Map tool...
  if(icon1)
    icon1 = WindowImage(sizex-2.0, 5.75, 3.0, 3.0, "FILE:=IMAP.RGB", StringLanguage(6), icon1)
  else
    icon1 = WindowImage(sizex-1.5, 6.25, 2.0, 2.0, "FILE:=IMAP.RGB", NULL, 0)
  if(icon1 == 255)
    // Close any existing map
    subwin = FindWindow(65535, "FILE:WAUTOMAP.RUN")
    if(subwin)
      subwin.stat00l = 255
    else
      // No map to close, so open a new map...
      ButtonSound()
      curx = SystemGet(SYS_CURSORSCREENPOS, 0, 0)
      cury = SystemGet(SYS_CURSORSCREENPOS, 0, 1)
      Spawn(WINDOW, curx, cury, 1.0, "FILE:WAUTOMAP.RUN")
    icon1 = 50


  // Quit game tool...
  if(CHEAT)
    if(icon2)
      icon2 = WindowImage(sizex-2.0, 8.75, 3.0, 3.0, "FILE:=ODCHANT.RGB", "Exit Game", icon2)
    else
      icon2 = WindowImage(sizex-1.5, 9.25, 2.0, 2.0, "FILE:=ODCHANT.RGB", NULL, 0)
    if(icon2 == 255)
      ButtonSound()
      GoPoof(POOF_ALL_OTHER_WINDOWS)
      Spawn(WINDOW, screenx / 2, 150.0, 1.0, "FILE:WMAIN.RUN")
      SystemSet(SYS_LEAVEGAME, 0, 0, 0)
      GoPoof(POOF_SELF)
      icon2 = 50


  // Super Secret Emacs tool...
  if(CHEAT)
    if(icon3)
      icon3 = WindowImage(sizex-2.0, 11.75, 3.0, 3.0, "FILE:=OUNHOLY.RGB", "Emacs", icon3)
    else
      icon3 = WindowImage(sizex-1.5, 12.25, 2.0, 2.0, "FILE:=OUNHOLY.RGB", NULL, 0)
    if(icon3 == 255)
      ButtonSound()
      child = Spawn(WINDOW, 200.0, 150.0, 1.0, "FILE:WEMACS.RUN")
      if(child)
        StringClear(child.string0)
        StringAppend(child.string0, "MAPGEN.SRC", 16)
      icon3 = 50


  // Monster spawner tool...
  if(CHEAT)
    if(icon4)
      icon4 = WindowImage(sizex-2.0,14.75, 3.0, 3.0, "FILE:=OANIMAT.RGB", "Monsters", icon4)
    else
      icon4 = WindowImage(sizex-1.5,15.25, 2.0, 2.0, "FILE:=OANIMAT.RGB", NULL, 0)
    if(icon4 == 255)
      ButtonSound()
      Spawn(WINDOW, 200.0, 150.0, 1.0, "FILE:WMONSTER.RUN")
      icon4 = 50



  // Secret draggable title bar...
  WindowBorder(TRUE, 0.0, 6.3, titlesize,  1, BORDER_NONE)



  // Limit position...
  self.x = offx
  if(self.y > max)
    self.y = max
  max = max * -7.5
  if(self.y < max)
    self.y = max

//-----------------------------------------------------------------------
