#define SMOKE_COLOR (128)
#define refresh_count  self.misc6

//-----------------------------------------------------------------------
int SmokeCloud(int count, float scale, float size, float sx, float sy, float sz)
  float x, y, z, vx, vy, vz, length


  while(count > 0)
    vx = 0.5 - (RollDice(1, 100) * 0.01)
    vy = 0.5 - (RollDice(1, 100) * 0.01)
    vz = 0.5 - (RollDice(1, 100) * 0.01)

    // Normalize
    length = sqrt(vx*vx + vy*vy + vz*vz)*2.0
    if(length > 0.0)
      vx = vx/length
      vy = vy/length
      vz = vz/length
    x = sx + vx
    y = sy + vy
    z = sz + vz
    PSMOKE.Effect(x, y, z, vx*scale, vy*scale, vz*scale, size, size*0.01, FALSE, SMOKE_COLOR, 255, 30+RollDice(1, 60))
    count--

//-----------------------------------------------------------------------
int AmmoEffect(int do_damage_to_target)
  int self, child, damage, wound

  self = FindSelf()
  if(do_damage_to_target)
    damage = RollDice(1, 3)+1
    wound = RollDice(1, 3)
    DamageTarget(DAMAGE_EDGE, damage, wound)


  self.x = self.lastx
  self.y = self.lasty
  self.z = self.lastz
  PSPARK.Effect(self.x, self.y, self.z, 10, 5)
  child = PEMBER.Effect(self.x, self.y, self.z, 0.0, 0.0, 0.0, 2.0, 128, 15, "FILE:=MPDUST.RGB")
  if(child)
    child.flags = PART_SPIN_ON
    child.red = SMOKE_COLOR>>16
    child.green = SMOKE_COLOR>>8
    child.blue = SMOKE_COLOR


//-----------------------------------------------------------------------
int Event()
  int self


  self = FindSelf()
  if(DEFLECT.Event(80, 1.0, FALSE))
    return TRUE
  if(self.event == EVENT_BLOCKED)
    AmmoEffect(FALSE)
    self.evtimer = 1  // Poof after all collisions checked...
    self.flags = PART_CHAR_COLLISION_ON
    return 0


  if(self.event == EVENT_SPAWNED_IN_WATER)
    PDROWN.Effect(self.x, self.y, self.z, 10)
    GoPoof(POOF_SELF)


  if(self.event == EVENT_HIT_CHARACTER)
    AmmoEffect(TRUE)


  if(self.event == EVENT_HIT_WALL || self.event == EVENT_HIT_FLOOR)
    AmmoEffect(FALSE)
    self.evtimer = 1  // Poof after all collisions checked...
    self.flags = PART_CHAR_COLLISION_ON
    return 0
  GoPoof(POOF_SELF)

//-----------------------------------------------------------------------
int Refresh()
  int self, index, owner, item_type, found, i
  float x, y, z, vx, vy, vz
  self = FindSelf()


  if(refresh_count == 0)
    owner = FindOwner()
    // Check owner...
    if(owner)
      // Look for some ammo to use...
      found = FALSE
      i = 224
      while(i < 246)
        if(i != 240 && i != 241)
          item_type = FileReadByte(owner, i)
          if(item_type >= ITEM_AMMO_10 && item_type <= ITEM_AMMO_1)
            found = TRUE
            if(item_type == ITEM_AMMO_1)
              item_type = ITEM_NONE
            else
              item_type++
            FileWriteByte(owner, i, item_type)
            i = 255
        i++


      if(found)
        // Pop a shell casing...
        PCASING.Effect(0, (self.x+owner.x)*0.5, (self.y+owner.y)*0.5, self.z)


        // Scootch shot to front of gonne for next update...
        self.misc0 = 120 // Length along weapon...  64 is at base...
        self.timer = 1
        refresh_count++
      else
        // Owner is out of ammo...
// !!!BAD!!!
// !!!BAD!!! Do a gonne click sound
// !!!BAD!!!
        ButtonSound()
// !!!BAD!!!
// !!!BAD!!!
// !!!BAD!!!
        owner.action = ACTION_GONNE_NO_AMMO
        GoPoof(POOF_SELF)
    else
      // No owner...
      GoPoof(POOF_SELF)
  else
    // Release the shot
    self.flags = PART_WALL_COLLISION_ON | PART_CHAR_COLLISION_ON | PART_FAST_ON // Removes PART_STUCK_ON flag...
    x = self.x
    y = self.y
    z = self.z + 1.0
    vx = self.vx
    vy = self.vy
    vz = self.vz
    SmokeCloud(20, 0.10, 1.0, x, y, z)
    PEXPLODE.Effect(x, y, z, (0.5*vx), (0.5*vy), 0.0, DAMAGE_FIRE, 0, 0, self.owner, self.team, NULL, SMOKE_COLOR, 32)
    PEMBER.Effect(x, y, z, (0.050*vx), (0.050*vy), 0.0, 1.0, 128, 8, "FILE:FIRE0.RGB")
    PEMBER.Effect(x, y, z, (0.100*vx), (0.100*vy), 0.0, 1.5, 192,12, "FILE:FIRE0.RGB")


    // Do the sound...
    PlaySound("FILE:BAD3.RAW", 225 + RollDice(1, 50), 255)




//-----------------------------------------------------------------------
int Spawn()
  int self, target
  self = FindSelf()



  self.image = NULL
  self.flags = PART_SPIN_ON
  self.length = 4.0
  self.width = 4.0
  self.timer = 1
  self.evtimer = 400 // Make sure it poofs before too long...
  refresh_count = 0


  target = FindTarget()
  if(target)
    // Right attack...
    AttachToTarget(2)

    // Modify the attachment matrix manually, so we get the particle
    // sittin' on the end of our bone...
    self.misc0 = 80 // Length along weapon...  64 is at base...
    self.misc1 = 64
    self.misc2 = 64
    self.misc3 = 45 // Should be less than misc0...  Used to determine directionality
    self.misc4 = 64
    self.misc5 = 64


    // Make it hit the ground if too low...
    self.colsize = 0.25


    // Set the facing of the particle so attack direction gets set correctly...
    self.spin = target.spin
  else
    // Shouldn't happen...
    GoPoof(POOF_SELF)


//-----------------------------------------------------------------------
int FrameEvent(float vx, float vy, float vz)
  int self, index, child
  self = FindSelf()


  index = FindIndex(self)
  SetSpawnTarget(index)
  SetSpawnOwner(index)
  SetSpawnTeam(self.team)
  child = Spawn(PARTICLE,  0.0, 0.0, 0.0, 0)
  if(child)
    child.vx = vx
    child.vy = vy
    child.vz = vz

//-----------------------------------------------------------------------

