#define CHEAT FALSE // (SystemGet(SYS_KEYDOWN, 282, 0))  // F1
#define SHOW_STAT_SUM FALSE


#define rand_seed      self.stat03l
#define hits           self.stat03h
#define mana           self.stat04l
#define strength       self.stat04h
#define dexterity      self.stat05l
#define intelligence   self.stat05h
#define roll_button    self.stat06l
#define unroll_button  self.stat06h
#define name_lock      self.stat07l
#define class_lock     self.stat07h
#define button1        self.stat08l
#define button2        self.stat08h
#define button3        self.stat09l
#define button4        self.stat09h
#define player         self.stat12l
#define class_list     self.stat13w
#define camx           self.stat14w
#define petrify        self.stat15w
#define num_class      self.stat16l
#define monster_mode   self.stat16h        
#define frame          self.stat17w
#define character_name self.string1

#define ACTION_TO_PLAY ACTION_STAND
#define ALTERNATE_ACTION ACTION_JUMP


// !!!MAKE SURE YOU ADD A NAME TO THE LIST IF YOU CHANGE THIS!!!
// !!!REMEMBER THE COMMA!!!
#define MAX_MONSTER_CLASS 26


// Model setup functions use team to determine character's eyes...
#define team           self.team

//-----------------------------------------------------------------------
PreviewString(int amount, int index, float x, float y)
  int length
  StringClear(String(0))
  StringAppend(String(0), StringLanguage(index), 9)
  length = StringLength(String(0))
  if(amount > 9)
    length++
  if(amount > 99)
    length++
  while(length < 7)
    StringAppend(String(0), " ", 9)
    length++
  StringAppendNumber(String(0), amount, 9)
  WindowString(WHITE, x, y, String(0))


//-----------------------------------------------------------------------
CorrectString(int string_to_correct)
  // Removes nonstandard characters from string...
  int i, value


  i = 0
  value = StringGetValue(string_to_correct, i)
  if(value >= 'a' && value <= 'z')
    value = value - 32
    StringSetValue(string_to_correct, i, value)
  while(i < 15)
    value = StringGetValue(string_to_correct, i)
    if(value > 0)
      if(value == 32)
        i++
        if(i < 15)
          value = StringGetValue(string_to_correct, i)
          if(value >= 'a' && value <= 'z')
            value = value - 32
            StringSetValue(string_to_correct, i, value)
        i--
      else
        if(value != 39 && value != '-')
          if(value < 65 || value > 122 || (value > 90 && value < 97))
            StringSetValue(string_to_correct, i, 32)
      i++ 
    else
      i = 16


//-----------------------------------------------------------------------
UnspaceString(int string_to_unspace)
  // Removes spaces from start & end of string...
  int i, value


  while(StringGetValue(string_to_unspace, 0) == 32)
    StringChopLeft(string_to_unspace, 1)


  i = StringLength(string_to_unspace)-1
  while(i > 0 && StringGetValue(string_to_unspace, i)==32)
    StringChopRight(string_to_unspace, 1)
    i--

//-----------------------------------------------------------------------
Spawn()
  int self, file, offset
  self = FindSelf()
  file = "FILE:CONFIG.DAT"
  player = self.z
  offset = (player<<5)+112
  team = TEAM_GOOD
  petrify = 0
  monster_mode = FALSE


  // Check for paying customers...
  num_class = 2
  if(SystemGet(SYS_PAYINGCUSTOMER, 0, 0))
    num_class = 8


  if(SystemGet(SYS_DEVTOOL, 0, 0))
    monster_mode = FileReadByte(file, 18+offset)
    if(monster_mode)
      num_class = MAX_MONSTER_CLASS




  // Read settings from the config file...
  name_lock = TRUE
  StringClear(character_name)
  StringAppend(character_name, file+offset, 16)
  if(StringLength(character_name) == 0)
    name_lock = FALSE


  // Continue reading config...
  class_list  = FileReadByte(file, 24+offset)
  if(class_list > num_class)
    class_list = 0
  class_lock = FALSE
  if(class_list != 0)
    // Load color values and class and stuff...
    class_lock = TRUE
    class_list  = (class_list - 1) << 8
    self.eqcol01 = FileReadByte(file, 16+offset)
    self.eqcol23 = FileReadByte(file, 17+offset)
    rand_seed   = FileReadByte(file, 22+offset)
  else
    // Give us a random character...
    class_list = (RollDice(1, num_class) - 1) << 8
    self.eqcol01 = RollDice(1, 256)
    self.eqcol23 = RollDice(1, 256)
    rand_seed   = RollDice(1, 256)
    // Make minority types occur one fourth of the time...
    if(RollDice(1, 100) < 50)
      self.eqcol01 = self.eqcol01 & 127
  if(name_lock == FALSE)
    // Give us a random name...
    ClassRandomName(character_name, class_list>>8)



  // Set up the preview model...
  camx = RollDice(1, 200)
  frame = 0


  // Fill in defaults
  hits = 0
  mana = 0
  strength = 0
  dexterity = 0
  intelligence = 0
  roll_button = 0
  unroll_button = 0
  button1 = 0
  button2 = 0
  button3 = 0
  button4 = 0

//-----------------------------------------------------------------------
ClassRandomName(int string_to_overwrite, int class)
  int alternate
  if(class > 7)
    class = 7
  if(class < 0)
    class = 0


  alternate = RollDice(1, 100)&1
  if(class == CLASS_SOLDIER)
    StringRandomName(string_to_overwrite, "FILE:SOLDIER0.TXT")
  if(class == CLASS_WIZARD)
    if(alternate)
      StringRandomName(string_to_overwrite, "FILE:WIZARD1.TXT")
    else
      StringRandomName(string_to_overwrite, "FILE:WIZARD0.TXT")
  if(class == CLASS_HEALER)
    if(alternate)
      StringRandomName(string_to_overwrite, "FILE:HEALER1.TXT")
    else
      StringRandomName(string_to_overwrite, "FILE:HEALER0.TXT")
  if(class == CLASS_GNOME)
    StringRandomName(string_to_overwrite, "FILE:GNOME0.TXT")
  if(class == CLASS_ELF)
    if(alternate)
      StringRandomName(string_to_overwrite, "FILE:ELF1.TXT")
    else
      StringRandomName(string_to_overwrite, "FILE:ELF0.TXT")
  if(class == CLASS_MYSTIC)
    StringRandomName(string_to_overwrite, "FILE:MYSTIC0.TXT")
  if(class == CLASS_DWARF)
    StringRandomName(string_to_overwrite, "FILE:DWARF0.TXT")
  if(class == CLASS_HUBBIN)
    StringRandomName(string_to_overwrite, "FILE:HUBBIN0.TXT")


//-----------------------------------------------------------------------
int ClassSetup(int class, int getscript)
  int modelfile
  int scriptfile
  int self


  self = FindSelf()
  ModelClear()
  if(class == CLASS_GNOME || class == CLASS_DWARF || class == CLASS_HUBBIN)
    modelfile = "FILE:PLAYERSM.RDY"
  else
    modelfile = "FILE:PLAYER.RDY"
  scriptfile = "FILE:CPLAYER.RUN"
  self.class = class
  CPLAYER.ModelSetup()
  if(getscript)
    return scriptfile
  return modelfile


//-----------------------------------------------------------------------
int MonsterSetup(int class, int getscript)
  int modelfile
  int scriptfile

  ModelClear()
  modelfile = "FILE:PLAYER.RDY"
  scriptfile = "FILE:CPLAYER.RUN"
  if(class == 0)
    modelfile = "FILE:PORCANIM.RDY"
    scriptfile = "FILE:CPORC.RUN"
    CPORC.ModelSetup()
  if(class == 1)
    modelfile = "FILE:LUMPANIM.RDY"
    scriptfile = "FILE:CLUMPKIN.RUN"
    CLUMPKIN.ModelSetup()
  if(class == 2)
    modelfile = "FILE:CARRION.RDY"
    scriptfile = "FILE:CCARRION.RUN"
    CCARRION.ModelSetup()
  if(class == 3)
    modelfile = "FILE:ZOMBANIM.RDY"
    scriptfile = "FILE:CZOMBI.RUN"
    CZOMBI.ModelSetup()
  if(class == 4)
    modelfile = "FILE:WHELP.RDY"
    scriptfile = "FILE:CWHELP.RUN"
    CWHELP.ModelSetup()
  if(class == 5)
    modelfile = "FILE:DRAGON.RDY"
    scriptfile = "FILE:CDRAGON.RUN"
    CDRAGON.ModelSetup()
  if(class == 6)
    modelfile = "FILE:BATANIM.RDY"
    scriptfile = "FILE:CBAT.RUN"
    CBAT.ModelSetup()
  if(class == 7)
    modelfile = "FILE:RAT.RDY"
    scriptfile = "FILE:CRAT.RUN"
    CRAT.ModelSetup()
  if(class == 8)
    modelfile = "FILE:KITTANIM.RDY"
    scriptfile = "FILE:CKITTEN.RUN"
    CKITTEN.ModelSetup()
  if(class == 9)
    modelfile = "FILE:GRUBBUG.RDY"
    scriptfile = "FILE:CGRUBBUG.RUN"
    CGRUBBUG.ModelSetup()
  if(class == 10)
    modelfile = "FILE:GRUBMUM.RDY"
    scriptfile = "FILE:CGRUBMUM.RUN"
    CGRUBMUM.ModelSetup()
  if(class == 11)
    modelfile = "FILE:GREMF.RDY"
    scriptfile = "FILE:CGREMF.RUN"
    CGREMF.ModelSetup()
  if(class == 12)
    modelfile = "FILE:TRANCH.RDY"
    scriptfile = "FILE:CTRANCH.RUN"
    CTRANCH.ModelSetup()
  if(class == 13)
    modelfile = "FILE:WRAITHE.RDY"
    scriptfile = "FILE:CWRAITHE.RUN"
    CWRAITHE.ModelSetup()
  if(class == 14)
    modelfile = "FILE:ROACHIE.RDY"
    scriptfile = "FILE:CROACHIE.RUN"
    CROACHIE.ModelSetup()
  if(class == 15)
    modelfile = "FILE:WARLOCK.RDY"
    scriptfile = "FILE:CWARLOCK.RUN"
    CWARLOCK.ModelSetup()
  if(class == 16)
    modelfile = "FILE:OGREBOD0.RDY"
    scriptfile = "FILE:COGRE.RUN"
    COGRE.ModelSetup()
  if(class == 17)
    modelfile = "FILE:CHEST.RDY"
    scriptfile = "FILE:CCHEST.RUN"
    CCHEST.ModelSetup()
  if(class == 18)
    modelfile = "FILE:PLAYER.RDY"
    scriptfile = "FILE:CMEDUSA.RUN"
    CMEDUSA.ModelSetup()
  if(class == 19)
    modelfile = "FILE:BATANIM.RDY"
    scriptfile = "FILE:CEYEBAT.RUN"
    CEYEBAT.ModelSetup()
  if(class == 20)
    modelfile = "FILE:LIZZANIM.RDY"
    scriptfile = "FILE:CLIZZER.RUN"
    CLIZZER.ModelSetup()
  if(class == 21)
    modelfile = "FILE:WUFFANIM.RDY"
    scriptfile = "FILE:CWUFF.RUN"
    CWUFF.ModelSetup()
  if(class == 22)
    modelfile = "FILE:LURKER.RDY"
    scriptfile = "FILE:CLURKER.RUN"
    CLURKER.ModelSetup()
  if(class == 23)
    modelfile = "FILE:SLIME.RDY"
    scriptfile = "FILE:CSLIME.RUN"
    CSLIME.ModelSetup()
  if(class == 24)
    modelfile = "FILE:WISP.RDY"
    scriptfile = "FILE:CWISP.RUN"
    CWISP.ModelSetup()
  if(class == 25)
    modelfile = "FILE:SKULANIM.RDY"
    scriptfile = "FILE:CSKULTON.RUN"
    CSKULTON.ModelSetup()
  if(getscript)
    return scriptfile
  return modelfile

//-----------------------------------------------------------------------
Event()
  int self, child, class, index, scriptfile, temp
  float x, y, z


  self = FindSelf()
  x = 0.0
  y = 0.0
  z = 0.0
  SystemSet(SYS_RANDOMSEED, 0, 0, rand_seed)
  child = 0
  class = class_list>>8



  if(monster_mode)
    scriptfile = MonsterSetup(class, TRUE)
  else
    scriptfile = ClassSetup(class, TRUE)
  SystemSet(SYS_GLOBALSPAWN, 0, GLOBAL_SPAWN_CLASS, class)
  SystemSet(SYS_GLOBALSPAWN, 0, GLOBAL_SPAWN_TEAM, self.team)
  SystemSet(SYS_GLOBALSPAWN, 0, GLOBAL_SPAWN_SUBTYPE, self.eqcol01)


  child = Spawn(CHARACTER, x, y, z, scriptfile)
  if(child)
    child.eqcol23 = self.eqcol23
    index = FindIndex(child)
    SystemSet(SYS_CHARFASTFUNCTION, index, 0, FAST_FUNCTION_MODELSETUP)


    StringClear(child.name)
    UnspaceString(character_name)
    if(StringGetValue(character_name, 0) == 0)
      // No name was enter'd...
      ClassRandomName(child.name, class)
    else
      // Give the desired name to the newly spawn'd character...
      StringAppend(child.name, character_name, 16)


    // Let the player control this character...
    SystemSet(SYS_LOCALPLAYER, player, 0, FindIndex(child))



//-----------------------------------------------------------------------
Refresh()
  int    i, self, file, offset, old_seed, child
  int    class_string, class, class_name, old_class
  int    openfile, scriptfile
  int    first_frame, max_frame
  int    skip
  int    slider_value
  int    read_offset, read_value
  int    action, weapon_frame
  float ratio

  // Allow us to save data in the window's properties...
  self = FindSelf()

  // screen aspect ratio
  ratio = SystemGet(SYS_SCREENPARAMS, 0, 2)
  ratio = ratio / SystemGet(SYS_SCREENPARAMS, 0, 3)


  // Build the string for the class drop down box...
  class = class_list>>8
  old_class = class
  class_string = String(1)
  if(monster_mode)
    // We're allowing the user to pick monster types...
    num_class = MAX_MONSTER_CLASS
    StringClear(class_string)
    StringAppend(class_string, "Porc,", 256)
    StringAppend(class_string, "Lumpkin,", 256)
    StringAppend(class_string, "Carrion,", 256)
    StringAppend(class_string, "Zombi,", 256)
    StringAppend(class_string, "Whelp,", 256)
    StringAppend(class_string, "Dragon,", 256)
    StringAppend(class_string, "Bat,", 256)
    StringAppend(class_string, "Rat,", 256)
    StringAppend(class_string, "Kitten,", 256)
    StringAppend(class_string, "Bug,", 256)
    StringAppend(class_string, "Mum,", 256)
    StringAppend(class_string, "Gremf,", 256)
    StringAppend(class_string, "Tranch,", 256)
    StringAppend(class_string, "Wraithe,", 256)
    StringAppend(class_string, "Roachie,", 256)
    StringAppend(class_string, "Warlock,", 256)
    StringAppend(class_string, "Ogre,", 256)
    StringAppend(class_string, "Mimic,", 256)
    StringAppend(class_string, "Medusa,", 256)
    StringAppend(class_string, "EyeBat,", 256)
    StringAppend(class_string, "Lizzer,", 256)
    StringAppend(class_string, "Wuff,", 256)
    StringAppend(class_string, "Lurker,", 256)
    StringAppend(class_string, "Slime,", 256)
    StringAppend(class_string, "Wisp,", 256)
    StringAppend(class_string, "Skulton,", 256)
  else
    // Only allow normal player classes...
    num_class = 2
    if(SystemGet(SYS_PAYINGCUSTOMER, 0, 0))
      num_class = 8
    StringClear(class_string)
    i = 0
    while(i < num_class)
      if(i == 4 && class == CLASS_ELF && (self.eqcol01&128))
        StringAppend(class_string, StringLanguage(107), 256)
      StringAppend(class_string, StringLanguage(84+i), 256)
      StringAppend(class_string, ",", 256)
      i++




  // Title bar
  StringClear(String(0))
  StringAppend(String(0), StringLanguage(3), 256)
  StringAppendNumber(String(0), player+1, 256)
  TitleBar(TRUE, 0.0, -5.5, String(0))



  // Main window
  WindowBorder(FALSE, -10.0, -5.5, 10 + 7.5 * ratio, 11, BORDER_FULL)



  // Setup the preview model...
  self.eqhelm = ITEM_NONE
  self.eqbody = ITEM_NONE
  self.eqlegs = ITEM_NONE
  self.eqleft = ITEM_NONE
  self.eqright = ITEM_NONE
  self.class = class
  if(monster_mode)
    openfile = MonsterSetup(class, FALSE)
    scriptfile = MonsterSetup(class, TRUE)
  else
    openfile = ClassSetup(class, FALSE)
    scriptfile = ClassSetup(class, TRUE)



  // Find the name of the selected class (by searching the drop down box string)
  i = 0
  class_name = class_string
  read_offset = 0
  read_value = StringGetValue(class_string, 0)
  while(i < class && read_offset < 255 && read_value != 0)
    if(read_value == ',')
      class_name = class_string+read_offset+1
      i++
    read_offset++
    read_value = StringGetValue(class_string, read_offset)
  StringClear(String(4))
  StringAppend(String(4), class_name, 16)
  class_name = String(4)
  i = 0
  while(i < 16)
    read_value = StringGetValue(class_name, i)
    if(read_value == ',' || read_value == 0)
      StringSetValue(class_name, i, 0)
    i++

  



  // Animate the model...
  action = ACTION_TO_PLAY
  first_frame = SystemGet(SYS_MODELACTIONSTART, openfile, action)
  if(first_frame == 65535)
    action = ALTERNATE_ACTION
    first_frame = SystemGet(SYS_MODELACTIONSTART, openfile, action)
  if(first_frame == 65535)
    frame = 0
  else
    max_frame = SystemGet(SYS_MODELMAXFRAME, openfile, 0)
    if(frame < first_frame)
      frame = first_frame
    skip = SystemGet(SYS_MAINFRAMESKIP, 0, 0)
    while(skip > 0)
      frame++
      skip--
    if(frame >= max_frame)
      frame = first_frame
    if(SystemGet(SYS_MODELFRAMEACTIONNAME, openfile, frame) != action)
      frame = first_frame



  // Preview window
  WindowBorder(FALSE, 0.0, -2.5, 7.5 * ratio, 8, BORDER_FULL)
  Window3DStart(0.0, 0.0, 7.5 * ratio, 8.0, FALSE)
  Window3DPosition(0.0, 0.0, 3.25, WIN_TARGET)
  Window3DPosition(sin(camx)*4.0, 3.25, 6.25, WIN_CAMERA)
  Window3DModel(WIN_3D_MODEL, MODEL_BASE_FILE, frame, 255)
  Window3DModel(WIN_3D_SHADOW, MODEL_BASE_FILE, frame, 255)
  Window3DModel(WIN_3D_MODEL, MODEL_LEGS_FILE, 0, 255)
  Window3DModel(WIN_3D_MODEL, MODEL_CHEST_FILE, 0, 255)
  Window3DModel(WIN_3D_MODEL, MODEL_ARMS_FILE, 0, 255)
  Window3DModel(WIN_3D_MODEL, MODEL_PADS_FILE, 0, 255)
  Window3DModel(WIN_3D_MODEL, MODEL_FEET_FILE, 0, 255)
  Window3DModel(WIN_3D_MODEL, MODEL_HEAD_FILE, 0, 255)
  Window3DModel(WIN_3D_MODEL, MODEL_HAIR_FILE, 0, 255)
  Window3DModel(WIN_3D_MODEL, MODEL_MOUTH_FILE, 0, 255)
  Window3DModel(WIN_3D_MODEL, MODEL_EYES_FILE, 0, 255)
  weapon_frame = SystemGet(SYS_MODELFRAMEFLAGS, openfile, frame) >> 7
  Window3DModel(WIN_3D_MODEL, MODEL_LEFT_FILE, weapon_frame, 255)
  Window3DModel(WIN_3D_MODEL, MODEL_RIGHT_FILE, weapon_frame, 255)
  Window3DModel(WIN_3D_MODEL, MODEL_LEFT2_FILE, weapon_frame, 255)
  Window3DModel(WIN_3D_MODEL, MODEL_RIGHT2_FILE, weapon_frame, 255)





  Window3DEnd()
  skip = SystemGet(SYS_MAINFRAMESKIP, 0, 0)
  while(skip > 0)
    camx++
    if(camx >= 360)
      camx = 0
    skip--



  // Go back to the main window...
  WindowBorder(FALSE, -10.0, -5.5, 20, 11, BORDER_NONE)



  // Random name button
  button1 = ButtonImage(button1, 16.0, 0.0, 2.0, 1.0, "?")
  if(button1 == 255)
    if(name_lock == FALSE && monster_mode == FALSE)
      ClassRandomName(character_name, class)
      ButtonSound()
    else
      BuzzerSound()



  // Name lock
  StringSetValue(String(0), 1, 0)
  if(name_lock)
    StringSetValue(String(0), 0, 254)
  else
    StringSetValue(String(0), 0, 240)
  button2 = ButtonImage(button2, 18.0, 0.0, 2.0, 1.0, String(0))
  if(button2 == 255)
    name_lock = (name_lock + 1) & 1
    ButtonSound()


  // Randomize class button
  button3 = ButtonImage(button3, 16.0, 1.0, 2.0, 1.0, "?")
  if(button3 == 255)
    if(class_lock == FALSE)
      self.eqcol01 = RollDice(1, 256)
      self.eqcol23 = RollDice(1, 256)
      rand_seed   = RollDice(1, 256)
      // Make minority types occur one fourth of the time...
      if(RollDice(1, 100) < 50)
        self.eqcol01 = self.eqcol01 & 127
      ButtonSound()
    else
      BuzzerSound()


  // Class lock
  if(class_lock)
    StringSetValue(String(0), 0, 254)
  else
    StringSetValue(String(0), 0, 240)
  button4 = ButtonImage(button4, 18.0, 1.0, 2.0, 1.0, String(0))
  if(button4 == 255)
    class_lock = (class_lock + 1) & 1
    ButtonSound()



  // Preview stats based on random seed...
  old_seed = SystemGet(SYS_RANDOMSEED, 0, 0)
  SystemSet(SYS_RANDOMSEED, 0, 0, rand_seed)
  hits = 0
  mana = 0
  strength = 0
  dexterity = 0
  intelligence = 0
  SystemSet(SYS_GLOBALSPAWN, 0, GLOBAL_SPAWN_TEAM, self.team)
  SystemSet(SYS_GLOBALSPAWN, 0, GLOBAL_SPAWN_SUBTYPE, self.eqcol01)
  if(!monster_mode)
    SystemSet(SYS_GLOBALSPAWN, 0, GLOBAL_SPAWN_CLASS, class)
  child = Spawn(CHARACTER, 0.0, 0.0, 0.0, scriptfile)
  if(child)
    // Using self.target overwrites self.int2425, which isn't used so it's okay...
    self.target = FindIndex(child)
    hits = child.hitsmax
    mana = child.manamax
    strength = child.str
    dexterity = child.dex
    intelligence = child.int
    GoPoof(POOF_TARGET_STUCK_PARTICLES)
    GoPoof(POOF_TARGET)  
  PreviewString(hits, 50, 0.0, 6.0)
  PreviewString(mana, 51, 0.0, 7.0)
  PreviewString(strength, 55, 0.0, 8.0)
  PreviewString(dexterity, 56, 0.0, 9.0)
  PreviewString(intelligence, 57, 0.0, 10.0)
  if(SystemGet(SYS_DEVTOOL, 0, 0) && CHEAT && SHOW_STAT_SUM)
    StringClear(String(0))
    StringAppendNumber(String(0), hits+mana+strength+dexterity+intelligence, 256)
    WindowString(WHITE, 10.0, 8.0, String(0))
    StringClear(String(0))


  // Name input...
  if(name_lock)
    WindowString(WHITE, 0.0, 0.0, character_name)
  else
    WindowInput(0.0, 0.0, 16, character_name, -1)
  CorrectString(character_name)



  // Show preview stats and sliders...
  if(class_lock)
    WindowString(WHITE, 0.0, 1.0, class_name)
  else
    slider_value = self.eqcol01 & 240
    slider_value = WindowSlider(0.0, 2.0, 9, 1, slider_value)
    if(class == 17)
      // Chest mimic...
      slider_value = 64
    self.eqcol01 = (self.eqcol01&15) + (slider_value&240)


    slider_value = (self.eqcol01&15) << 4
    slider_value = WindowSlider(0.0, 3.0, 9, 1, slider_value)
    self.eqcol01 = (self.eqcol01&240) + (slider_value>>4)

    slider_value = self.eqcol23 & 240
    slider_value = WindowSlider(0.0, 4.0, 9, 1, slider_value)
    self.eqcol23 = (self.eqcol23&15) + (slider_value&240)

    slider_value = (self.eqcol23&15) << 4
    slider_value = WindowSlider(0.0, 5.0, 9, 1, slider_value)
    self.eqcol23 = (self.eqcol23&240) + (slider_value>>4)


    unroll_button = ButtonImageVertical(unroll_button, 8.0, 6.0, 1.0, 2.5, "<")
    if(unroll_button == 255)
      rand_seed--
      ButtonSound()
    roll_button = ButtonImageVertical(roll_button, 8.0, 8.5, 1.0, 2.5, ">")
    if(roll_button == 255)
      rand_seed++
      ButtonSound()


    // Class selection thing...
    class_list = WindowMiniList(0.0, 1.0, 16, num_class, class_list, class_string)



  // Restore the random seed...
  SystemSet(SYS_RANDOMSEED, 0, 0, old_seed)



  // Give a new random name if class was changed...
  class = class_list>>8
  if(class != old_class)
    if(name_lock == FALSE && monster_mode == FALSE)
      ClassRandomName(character_name, class)
  if(class >= num_class)
    class = num_class-1
    class_list = class<<8



  // Save the settings...
  file = "FILE:CONFIG.DAT"
  offset = (player<<5) + 112
  if(name_lock)
    StringClear(file+offset)
    StringAppend(file+offset, character_name, 16)
  else
    StringClear(file+offset)
  if(class_lock)
    FileWriteByte(file, offset+16, self.eqcol01)
    FileWriteByte(file, offset+17, self.eqcol23)
    FileWriteByte(file, offset+18, monster_mode)
    FileWriteByte(file, offset+22, rand_seed)
    FileWriteByte(file, offset+24, (class_list>>8)+1)
  else
    FileWriteByte(file, offset+18, 0)
    FileWriteByte(file, offset+24, 0)




  // Team select cheat & monster cheat...
  if(SystemGet(SYS_DEVTOOL, 0, 0) && CHEAT)
    if(team == TEAM_GOOD)
      if(ButtonImage(0, -0.5, -2.5, 6.0, 2.0, "Good") == 255)
        team = TEAM_MONSTER
    else
      if(ButtonImage(0, -0.5, -2.5, 6.0, 2.0, "Mons") == 255)
        team = TEAM_GOOD
    if(monster_mode)
      if(ButtonImage(0, 14.5, -2.5, 6.0, 2.0, "Mons") == 255)
        monster_mode = FALSE
    else
      if(ButtonImage(0, 14.5, -2.5, 6.0, 2.0, "Human") == 255)
        monster_mode = TRUE


//-----------------------------------------------------------------------
