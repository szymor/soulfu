#define button_one    self.stat00l
#define testing       self.stat00h
#define timer         self.stat01w

//-----------------------------------------------------------------------
Spawn()
  int self, file
  self = FindSelf()
  file = "FILE:CONFIG.DAT"


  button_one = 0
  testing = FileReadByte(file, 73)
  if(testing)
    // Give the user some time to click the okay button...
    timer = 1000
  else
    timer = 20


//-----------------------------------------------------------------------
Refresh()
  int    i, self, file, count, testing
  float midx

  // Allow us to save data in the window's properties...
  self = FindSelf()
  file = "FILE:CONFIG.DAT"
  testing = FileReadByte(file, 73)
  midx = SystemGet(SYS_SCREENPARAMS, 0, 2) / 2

  if(!testing)
    if(timer == 0)
      Spawn(WINDOW, midx, 150.0, 1.0, "FILE:WMAIN.RUN")
      Spawn(WINDOW, midx, 150.0, 1.0, "FILE:WSPLASH.RUN")
      GoPoof(POOF_SELF)
    timer--
    return 0



  // Button actions
  if(button_one == 250)
    // User accepted display changes...
    FileWriteByte(file, 73, 0)   // Testing Off
    ButtonSound()
    Spawn(WINDOW, midx, 150.0, 1.0, "FILE:WMAIN.RUN")
    GoPoof(POOF_SELF)
  if(SystemGet(SYS_KEYPRESSED, 27, 0))  // Escape key
    timer = 0
  if(timer == 0)
    // Time out...  Assume display is broken...  Reset to defaults...
    FileWriteByte(file, 68, 3)   // 640x480
    FileWriteByte(file, 69, 16)  // 16 bit rgb
    FileWriteByte(file, 70, 16)  // 16 bit z buffer
    FileWriteByte(file, 72, 1)   // Fullscreen on
    FileWriteByte(file, 73, 0)   // Testing Off
    SystemSet(SYS_QUITGAME, 0, 0, TRUE)


  // Title bar (can't drag)
  WindowBorder(FALSE, -3.5, -2.85, 7,  1, BORDER_TITLE)
  WindowString(WHITE, 0.0, -0.1, StringLanguage(214))



  // Main window
  WindowBorder(FALSE, -8.0, -1.15, 16, 4, BORDER_FULL)
  WindowString(WHITE, 0.0, 0.0, StringLanguage(215))
  StringClear(String(0))
  StringAppend(String(0), StringLanguage(216), 256)
  StringAppendNumber(String(0), (timer/60), 256)
  WindowString(RED, 0.0, 1.0, String(0))


  // Button images
  if(button_one < 250)
    button_one = WindowImage(0.0, 2.0, 16.0, 2.0, "FILE:BUTTON.RGB", NULL, 0)
    WindowString(WHITE, 3.0, 2.5, StringLanguage(217))
    count = SystemGet(SYS_MAINFRAMESKIP, 0, 0)
    i = 0
    while(i < count)
      timer--
      i++
  else
    WindowImage(16.0, 4.0, -16.0, -2.0, "FILE:BUTTON.RGB", NULL, 0)
    WindowString(WHITE, 3.2, 2.7, StringLanguage(217))
    button_one--


//-----------------------------------------------------------------------
