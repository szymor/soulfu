#define button_one    self.stat00l
#define button_two    self.stat00h
#define button_three  self.stat01l
#define button_four   self.stat01h
#define level         self.stat02l

//-----------------------------------------------------------------------
Spawn()
  int self
  ITEMREG.Setup()
  self = FindSelf()
  button_one = 0
  button_two = 0
  button_three = 0
  button_four = 0
  level = 0

//-----------------------------------------------------------------------
RowIcon(float x, float y, int offset, int openfile, int allow_copy, int allow_all)
  int item, image, overlay, left, right, copy_left, copy_right

  item = FileReadByte(openfile, offset%192)
  image = SystemGet(SYS_ITEMREGISTRYICON, item, 0)
  overlay=SystemGet(SYS_ITEMREGISTRYOVERLAY, item, 0)
  StringClear(NAME_STRING)
  SystemGet(SYS_ITEMREGISTRYNAME, item, 0)
  if(image)
    WindowImage(x+0.5, y, 3.0, 3.0, image, NULL, 0)
  if(overlay)
    WindowImage(x+1.25, y+0.75, 1.5, 1.5, overlay, NULL, 0)


  copy_left = WindowImage(x+0.5, y, 1.5, 3.0, NULL, NAME_STRING, 50)
  copy_right = WindowImage(x+2.0, y, 1.5, 3.0, NULL, NAME_STRING, 50)



  left = ButtonImage(0, x+0.5, y+3.0, 1.5, 1.0, "<")
  right = ButtonImage(0, x+2.0, y+3.0, 1.5, 1.0, ">")


  if(left == 255)
    item--
    if(!allow_all)
      if(item == ITEM_NONE)
        item--
      if(item >= ITEM_QUIVER_10 && item <= ITEM_AMMO_1)
        // Item is ammo of some sort
        item = item - ((item-ITEM_QUIVER_10) % 10)
      if(item >= ITEM_DAGGER && item <= (ITEM_WAR_HAMMER+7))
        // Item is a weapon
        item = (item & 248)
      if(item >= ITEM_SHIELD && item <= (ITEM_LEGS_5+3))
        // Item is an armor piece
        item = (item & 252)



  if(right == 255)
    item++
    if(!allow_all)
      if(item == ITEM_NONE)
        item++
      if(item >= ITEM_QUIVER_10 && item <= ITEM_AMMO_1)
        // Item is ammo of some sort
        if(((item - ITEM_QUIVER_10) % 10) > 0)
          item = item - ((item-ITEM_QUIVER_10) % 10) + 10
      if(item >= ITEM_DAGGER && item <= (ITEM_WAR_HAMMER+7))
        // Item is a weapon
        item = (item & 248) + 8
      if(item >= ITEM_SHIELD && item <= (ITEM_LEGS_5+3))
        // Item is an armor piece
        item = (item & 252) + 4


  if(allow_copy)
    if(copy_left == 255)
      item = FileReadByte(openfile, ((offset-1)&7) + (offset&248))

    if(copy_right == 255)
      item = FileReadByte(openfile, ((offset+1)&7) + (offset&248))
  else
    if(copy_left == 255 || copy_right == 255)
      item = ITEM_NONE



  if((left == 255) || (right == 255) || (copy_left == 255) || (copy_right == 255))
    FileWriteByte(openfile, offset%192, item)


//-----------------------------------------------------------------------
Refresh()
  int    i, j, self, offset


  // Allow us to save data in the window's properties...
  self = FindSelf()


  // Button actions
  if(button_one == 250)
    ButtonSound()
    level--
    if(level == 255)
      level = 5
  if(button_two == 250)
    ButtonSound()
    level++
    if(level == 6)
      level = 0
  if(button_three == 250)
    ButtonSound()
    UpdateFiles(UPDATE_SDFSAVE)
  if(button_four == 250)
    ButtonSound()
    GoPoof(POOF_SELF)


  // Title bar
  TitleBar(TRUE, 0.0, -10.0, "Random Item Tool")


  // Main window
  WindowBorder(FALSE, -16.0, -10.0, 32, 20, BORDER_FULL)


  // Button images
  button_one = ButtonImage(button_one, 0.0, 0.0, 6.0, 2.0, "<<")
  button_two = ButtonImage(button_two,26.0, 0.0, 6.0, 2.0, ">>")
  button_three = ButtonImage(button_three,0.0,18.0,16.0,2.0, "SDF Save")
  button_four = ButtonImage(button_four,16.0,18.0,16.0,2.0, "GoPoof")


  // Level text
  StringClear(String(0))
  StringAppend(String(0), "Level ", 256)
  StringAppendNumber(String(0), level+1, 256)
  CenterString(WHITE, 16.0, 0.5, String(0))


  // Item slots...
  offset = level<<5
  i = 0
  while(i < 4)
    j = 0
    while(j < 8)
      RowIcon(j*4.0, 2.0+(i*4.0), offset, "FILE:RANDITEM.DAT", TRUE, FALSE)
      offset++
      j++
    i++

//-----------------------------------------------------------------------
