// Dropped item particle...  Used for collectable
// food & potions...  Also used for bouncie debris...
#define item_type   self.misc6
#define hit_count   self.misc7

#define TRAIL_SMOKE_COLOR (8421504)
#define FIRE_SMOKE_COLOR (128)
#define ICE_SMOKE_COLOR  (4239600)

//-----------------------------------------------------------------------
int Effect(float x, float y, float z, float vx, float vy, float vz, float size, int image, int item)
  int child

  child = Spawn(PARTICLE, x, y, z, 0)
  if(child)
    child.image = image
    child.misc6 = item
    child.vx = vx
    child.vy = vy
    child.vz = vz
    child.length = size
    child.width = size
    child.colsize = 0.5 * size
  return child

//-----------------------------------------------------------------------
int Refresh()
  int self

  // Fade away...
  self = FindSelf()
  self.timer = 1
  self.alpha = self.alpha-3
  if(self.alpha < 15)
    GoPoof(POOF_SELF)

//-----------------------------------------------------------------------
int Event()
  int self, target, owner, chance, child, charge


  self = FindSelf()
  if(self.event == EVENT_HIT_WATER)
    STANDARD.PartWaterEvent(self.length)
    if(item_type == ITEM_EXPLOSIVES || item_type == ITEM_SUPER_HEAL || item_type == ITEM_SUPER_MANA)
      if(RoomWaterType() == WATER_TYPE_LAVA)
        charge = 128
        if(item_type == ITEM_EXPLOSIVES)
          charge = 255
        PEXPLODE.Effect(self.x, self.y, self.z, 0.0, 0.0, (0.013*charge)+0.025, DAMAGE_FIRE, (charge>>5), 1, self.owner, self.team, "FILE:FIRE1.RGB", FIRE_SMOKE_COLOR, charge)
        GoPoof(POOF_SELF)
  if(self.event == EVENT_BLOCKED)
    if(!(self.flags & PART_SLOW_ON))
      // Particle is in the air yet...  Bounce off of shield...
      self.x = self.lastx
      self.y = self.lasty
      self.vx = -self.vx
      self.vy = -self.vy
  if(self.event == EVENT_TIMER)
    // Smoking/explodie particle
    self.evtimer = 1
    PSMOKE.Effect(self.x, self.y, self.z, 0.0, 0.0, 0.125, 0.50, 0.04, FALSE, TRAIL_SMOKE_COLOR, 128, 50)
  if((self.event == EVENT_HIT_CHARACTER || self.event == EVENT_DEFLECTED) && self.evtimer == 0)
    if(item_type == ITEM_TRIPE)
      // Special handling for tripe...
      target = FindTarget()
      if(target)
        if(target.aitimer > 0 && target.rider >= MAX_CHARACTER && target.pttimer == 0)
          if(target.vflags & VIRTUE_FLAG_EATS_TRIPE)
            // Try to convert monsters into helpers...
            owner = FindOwner()
            if(owner)
              if(owner.team != target.team)
                // Compassion earned for helping monsters...
                ExperienceFunction(self.owner, EXPERIENCE_VIRTUE_COMPASSION, 5, FALSE)


                // Conversion only works for paying people...
                if(SystemGet(SYS_PAYINGCUSTOMER, 0, 0))
                  // Tougher monsters less likely to convert...
                  chance = owner.hitsmax-target.hits-(target.hitsmax/5)
                  // Elf is better at taming than others
                  if (owner.class == CLASS_ELF)
                    chance = chance + 5
                    if(chance > 30)
                      chance = 30
                  else
                    if(chance > 20)
                      chance = 20
                  if(chance < 1)
                    chance = 1
                  StringClear(String(0))
                  StringAppend(String(0), "Percent Chance == ", 256)
                  StringAppendNumber(String(0), chance*5, 256)
                  DebugMessage(String(0))
                  if(ChildCount(owner) < MaxHelpers(owner, 2) && RollDice(1, 20) <= chance)
                    // It worked, by golly...
                    target.team = owner.team
                    target.owner = self.owner
                    target.level = 1
                    target.exp = 0
                    target.item00 = ITEM_NONE
                    target.item01 = ITEM_NONE
                    target.item02 = ITEM_NONE
                    target.item03 = ITEM_NONE
                    target.item04 = ITEM_NONE
                    target.item05 = ITEM_NONE
                    SystemSet(SYS_MAPOBJECTDEFEATED, 0, 0, self.target)
                    SystemSet(SYS_CHARFASTFUNCTION, self.target, 0, FAST_FUNCTION_MODELSETUP)
                    SystemSet(SYS_CHARFASTFUNCTION, self.target, 0, FAST_FUNCTION_DEFENSERATING)
                    child = PSMOKE.Effect(target.x, target.y, target.z+(target.height*0.5), 0.0, 0.0, 0.0, (target.boxsize+target.height+2.0), 0.985, TRUE, WHITE, 255, 25)
                    if(child)
                      child.image = "FILE:=HEART.RGB"
                      child.spin = 0
                      child.vspin = 0
              else
                // Compassion earned for helping pets...
                if(target.hits < target.hitsmax)
                  ExperienceFunction(self.owner, EXPERIENCE_VIRTUE_COMPASSION, 5, FALSE)



            // Heal regardless of whether convert worked or not...
            RestoreCharacterHits(target, 10, 5)
            PFLASH.Effect(target, "FILE:SPARK4.RGB", GREY, TRUE, 0.75)


            // Do the effects...
            PlaySound("FILE:COLLECT.RAW", 256, 128)
            PCOLLECT.Effect(self.x, self.y, self.z, 0.1, 0.5, 5, 255)
            GoPoof(POOF_SELF)
    if(item_type != ITEM_NONE)
      target = STANDARD.ParticleGrabTarget()
      if(target)
        if(target.pttimer == 0)
          if(GiveItemToCharacter(target, item_type))
            PITEM.Effect(target, item_type, self.x, self.y, self.z + 2.5)
            PlaySound("FILE:COLLECT.RAW", 256, 128)
            PCOLLECT.Effect(self.x, self.y, self.z, 0.1, 0.5, 5, 255)
            GoPoof(POOF_SELF)
  if(self.event == EVENT_HIT_WALL || self.event == EVENT_HIT_FLOOR)
    self.x = self.lastx
    self.y = self.lasty

    if(self.evtimer > 0)
      // Particle is smokin', that means it'll explode...
      charge = 128
      if(item_type == ITEM_EXPLOSIVES)
        charge = 255
      if(self.dtype == DAMAGE_ICE)
        PEXPLODE.Effect(self.x, self.y, self.z, 0.0, 0.0, (0.013*charge)+0.025, DAMAGE_ICE,  (charge>>5), 1, self.owner, self.team, "FILE:ICE1.RGB",  ICE_SMOKE_COLOR,  charge)
      else
        PEXPLODE.Effect(self.x, self.y, self.z, 0.0, 0.0, (0.013*charge)+0.025, DAMAGE_FIRE, (charge>>5), 1, self.owner, self.team, "FILE:FIRE1.RGB", FIRE_SMOKE_COLOR, charge)
      PSPARK.Effect(self.lastx, self.lasty, self.lastz, 8, 7)
      GoPoof(POOF_SELF)
    else
      if(self.spin < 32768)
        self.vspin = self.spin
        self.spin = self.spin*0.75
        self.vspin = (self.spin - self.vspin)*0.125
      else
        self.vspin = self.spin
        self.spin = self.spin*0.75 + 65536*0.25
        self.vspin = (self.spin - self.vspin)*0.125
      if(self.event == EVENT_HIT_WALL)
        self.z = self.lastz
        self.vx = self.vx * -0.75
        self.vy = self.vy * -0.75
      else
        if((self.z-self.colsize) < RoomWaterLevel())
          self.vz = 0.0
        self.z = self.lastz
        self.vx = self.vx * 0.75
        self.vy = self.vy * 0.75
        if(self.vz < 0.0)
          self.vz = self.vz * -0.5
        hit_count++
        if(hit_count > 4)
          self.vx = 0
          self.vy = 0
          self.vz = 0
          self.flags = PART_SPIN_ON | PART_SLOW_ON | PART_CHAR_COLLISION_ON | PART_HIT_OWNER_ON
          self.vspin = 0
          self.spin = 0
          if(self.timer == 0)
            if(item_type == ITEM_NONE)
              self.timer = 150 + RollDice(1, 30)
            else
              self.timer = 2000 + RollDice(1, 30)



//-----------------------------------------------------------------------
int Spawn()
  int self
  self = FindSelf()
  self.length = 1.00
  self.width = 1.00
  self.colsize = 0.5
  self.flags = PART_SPIN_ON | PART_WALL_COLLISION_ON | PART_GRAVITY_ON
  self.spin = RollDice(1, 256)<<8
  if(RollDice(1, 100) > 50)
    self.vspin = 2000
  else
    self.vspin = -2000
  self.alpha = 254
  hit_count = 0

//-----------------------------------------------------------------------
