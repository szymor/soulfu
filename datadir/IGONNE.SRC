//-----------------------------------------------------------------------
int GetName()
  STANDARD.ItemName("GONNE", NULL, DAMAGE_EDGE, 2, 4, 1, 3)


//-----------------------------------------------------------------------
// Called to put item in character's hand
int ModelSetup()
  int self, grip

  grip = SystemGet(SYS_WEAPONGRIP, 0, 0)
  if(grip == MODEL_RIGHT_FILE)
    self = FindSelf()

    ModelAssign(self+grip, "FILE:GONNE.RDY")
    ModelAssign(self+grip+4, "FILE:DULL.RGB")
    ModelAssign(self+grip+8, "FILE:=GONNE.RGB")
    ModelAssign(self+grip+12, "FILE:WOODSHIN.RGB")
    ModelAssign(self+grip+16, "FILE:=GONNE.RGB")
    ModelAssign(self+grip+20, WHITE)


//-----------------------------------------------------------------------
// Called when attack particle is supposed to be spawned
int FrameEvent()
  int self, dex
  float vx, vy, vz


  self = FindSelf()
  dex = self.dex
  if(dex > 50)
    dex = 50


  // Aim at the best enemy target...
  AutoAim(1.5, 0.0, self.spin, self.team, dex, 8192 + (164*dex), AUTOAIM_CRUNCH)
  vx = AutoAim(0.0, 0.0, 0, 0, 0, 0, AUTOAIM_X)
  vy = AutoAim(0.0, 0.0, 0, 0, 0, 0, AUTOAIM_Y)
  vz = AutoAim(0.0, 0.0, 0, 0, 0, 0, AUTOAIM_Z)
  PGONNE.FrameEvent(vx, vy, vz)

//-----------------------------------------------------------------------
// Called when item is first used, to start proper animation
int Event()
  int self, grip, reload_time, dex, i, found, item_type, index

  // Modify reload time by dexterity...
  self = FindSelf()
  grip = SystemGet(SYS_WEAPONGRIP, 0, 0)
  reload_time = WEAPON_RELOAD_LONG
  dex = self.dex
  if(dex > 50)
    dex = 50
  reload_time = reload_time - ((dex*reload_time)/50)


  if(self.action == ACTION_WALK || self.action == ACTION_STAND || self.action == ACTION_JUMP || self.action == ACTION_DOUBLE)
    if(grip == MODEL_RIGHT_FILE)
      if(self.rrtimer == 0)


        // Check for ammo in inventory...
        found = FALSE
        i = 224
        while(i < 246)
          if(i != 240 && i != 241)
            item_type = FileReadByte(self, i)
            if(item_type >= ITEM_AMMO_10 && item_type <= ITEM_AMMO_1)
              found = TRUE
              // Long reload on odd shots
              if(item_type & 1)
                self.rrtimer = reload_time
              i = 255
          i++


        // Fire off a shot...
        self.action = ACTION_GONNE_RIGHT
        self.daction = ACTION_STAND
        ButtonHandled()

//-----------------------------------------------------------------------
